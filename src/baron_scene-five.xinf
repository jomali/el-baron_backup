

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena quinta
!!
!!==============================================================================
!!
!!	Archivo:		baron_scene-five.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2019/02/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2019, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	0)
!!
!!------------------------------------------------------------------------------

[ EndSceneFive;
];

!! @Paredes
Atrezzo	Paredes "paredes" Limbo
 has	female pluralname,
 with	found_in Comedor Edificio PatioArmas Sotano,
		name_f 'pared',
		name_fp 'paredes',
		name_m 'muro',
		name_mp 'muros',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'ennegrecida' 'ennegrecidas' 'ennegrecido' 'ennegrecidos',
		!!------------------
		description [;
			print "La piedra de las paredes parece caliza. En algún momento en el pasado estos muros mostraron sin duda un aspecto blanquecino e inmaculado, pero ahora están ennegrecidos y cascados por culpa de algún fuego.";
			new_line;
			return true;
		],
;

!!==============================================================================
!!
!!	1)	Patio de armas
!!
!!------------------------------------------------------------------------------

!! @DirPatioArmas
AdjacentRoom	DirPatioArmas "patio"
 has	male,
 with	found_in Edificio Torre,
		name_m 'claustro' 'patio',
		gender G_MASCULINO,
		adjectives 'armas' 'interior',
		!!------------------
		adjacent_room PatioArmas,
		before [;
			Enter:
				self.AdjacentRoom::before();
				return true;
		],
;

!! @PatioArmas
Outdoors	PatioArmas
 has	male,
 with	short_name [;
			print "[](Patio) de armas";
			return true;
		],
		name_m 'claustro' 'patio',
		gender G_MASCULINO,
		adjectives 'armas' 'interior',
		!!------------------
		description [;
			print "Detrás de la fachada aparentemente intacta se oculta una verdadera ruina. Casi todas las construcciones del castillo han sucumbido y en su lugar quedan tan sólo grandes montones de escombro semicubiertos por la nieve entre los que las tres torres parecen brotar como hongos gigantescos de sillería y argamasa. Hay un sendero improvisado que serpentea entre las piedras, comunicando la entrada del castillo con un corto [DirTorre] al interior de la gran torre central y con el otro único [DirEdificio] que parece permanecer en pie.";
			new_line;
			return true;
		],
;

!! @Estatua
HeavyItem	-> Estatua "estatua"
 has	female,
 with	name_f 'efigie' 'escultura' 'estatua' 'figura' 'talla',
		name_m 'hombre' 'monumento',
		gender G_FEMENINO,
		adjectives 'desgastada' 'desgastado' 'granito' 'ilustre',
		!!------------------
		!! TODO - Hablar con ella
		before [;
			Take,
			Turn,
			Pull,
			Push,
			PushDir:
				print "Es con mucho demasiado grande y pesada.";
				new_line;
				return true;
		],
		describe [;
			new_line;
			print "[Una self] domina el centro del patio.";
			new_line;
			return true;
		],
		description [;
			print "Tiene al menos cuatro varas y media de altura. Representa la figura de un hombre de porte ilustre, pero el granito está tan desgastado por los elementos que los rasgos de su rostro se han perdido en el tiempo.";
			new_line;
			return true;
		],
;

!! @PatioArmasSendero
Atrezzo	-> PatioArmasSendero "sendero"
 has	male,
 with	name_m 'camino' 'sendero' 'rastro',
		gender G_FEMENINO,
		adjectives 'improvisado',
		!!------------------
		description [;
			print "Un rastro dejado por alguien en la nieve. Forma una T algo irregular, comunicando la entrada del castillo con un tramo de [DirTorre] al interior de la gran torre central y con el otro único [DirEdificio] que permanece en pie.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	2)	Edificio semiderruido
!!
!!------------------------------------------------------------------------------

!! @DirEdificio
AdjacentRoom	DirEdificio "edificio"
 has	male,
 with	found_in Comedor PatioArmas,
		name_m 'construccion' 'edificio',
		gender G_MASCULINO,
		adjectives 'semiderruido',
		!!------------------
		adjacent_room Edificio,
		before [;
			Enter:
				if (location == PatioArmas) {
					print "Entras en la construcción a través de un agujero enorme abierto en la pared.";
					new_line;
				}
				self.AdjacentRoom::before();
				return true;
		],
;

!! @Edificio
Outdoors	Edificio
 has	male,
 with	short_name [;
			print "[](Edificio) semiderruido";
			return true;
		],
		name_m 'construccion' 'edificio',
		gender G_MASCULINO,
		adjectives 'semiderruido',
		!!------------------
		description [;
			print "[El EdificioTejado] se ha desplomado completamente en el interior y las paredes han comenzado a desmoronarse, cubriendo el suelo con pedazos de yeso y de mampostería, tejas partidas y vigas de madera caídas que ahora ya sólo sostienen una fina capa de nieve. El [self](edificio) parece formar parte de las antiguas dependencias del servicio. El lugar en el que en el pasado los criados del barón vivieron, rieron y dónde hicieron el amor, tal vez. Ahora no es más que una vieja ruina.
			^^
			A través del hueco abierto en el muro se ve [el DirPatioArmas]. Hay además [un DirComedor]";
			!! TODO
			if (true) {
				print " oscuro, como un rectángulo de tinta negra";
			} else {
				print " sin puerta, desde el que se percibe un débil resplandor";
			}
			print ", en una de las paredes que aún permanecen en pie.";
			new_line;
			return true;
		],
;

!! @EdificioTejado
Atrezzo	-> EdificioTejado "tejado"
 has	male,
 with	name_f 'techumbre' 'teja' 'viga',
		name_fp 'tejas' 'vigas',
		name_m 'techo' 'tejado',
		gender G_MASCULINO,
		adjectives 'caido' 'derrumbado',
		!!------------------
		description [;
			print "El tejado se ha derrumbado por completo sobre el suelo del edificio y ahora la fría luz de [la Luna] se cuela libre de obstáculos.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	3)	En el comedor
!!
!!------------------------------------------------------------------------------

!! @DirComedor
AdjacentRoom	DirComedor "umbral"
 has	male,
 with	found_in Edificio,
		name_f 'puerta',
		name_m 'comedor' 'umbral' 'salon',
		gender G_MASCULINO,
		!!------------------
		adjacent_room Comedor,
		before [;
			Enter:
				self.AdjacentRoom::before();
				return true;
		],
		!! TODO - Contemplar el resplandor de la antorcha si está encendida
		description [;
			print "No hay ninguna puerta ocupando ese [self] desde hace mucho tiempo. Se abre como un bostezo a la oscuridad de más allá.";
			new_line;
			return true;
		],
;

!! @Comedor
Indoors	Comedor
 has	male,
 with	short_name [;
			print "En el [](comedor)";
			return true;
		],
		name_f 'estancia' 'sala',
		name_m 'comedor' 'salon',
		gender G_MASCULINO,
		!!------------------
		description [;
			print "Este salón se ha mantenido en mejores condiciones que [el DirEdificio] contiguo. Aún así, varios agujeros en el techo dejan el paso libre a la nívea luz de la luna y ves cómo se han empezado a formar pequeños montones de nieve semifundida bajo ellos. No hay muebles más allá de una larga [ComedorMesa](mesa:REMARKABLE_OBJECT) de madera, con una silla en cada extremo.";
			new_line;
			return true;
		],
;

!! @Antorcha
Item	-> Antorcha "antorcha"
 has	female light,
 with	name_f 'antorcha' 'tea',
		gender G_FEMENINO,
		adjectives 'apagada' 'encendida',
		!!------------------
		describe [;
			if (self has light && self hasnt moved) {
				new_line;
				print "Sujeta a un aplique en la pared, una antorcha arde débilmente y arroja algo de luz y calor en la fría estancia.";
				new_line;
				return true;
			}
		],
		description [;
			if (self has light) {
				print "La luz amarillenta de su llama contrasta en el blanco y negro de la noche.";
			} else {
				print "El lienzo engrasado del extremo se ha consumido por completo. Ahora es sólo un pedazo de madera ennegrecida y fría.";
			}
			new_line;
			return true;
		],
;

!! @ComedorNieve
Atrezzo	-> ComedorNieve
 has	female,
 with	short_name [;
			print "montones de [](nieve)";
			return true;
		],
		name_f 'nieve',
		gender G_FEMENINO,
		adjectives 'montones',
		!!------------------
		before [;
			Dig,
			LookUnder,
			Search:
				print "Bajo la nieve tan sólo se oculta el frío suelo del comedor.";
				new_line;
				return true;
		],
		description [;
			print "Tres pequeños montones de nieve se han formado bajo los agujeros del techo.";
			new_line;
			return true;
		],
;

!! @ComedorMesa
HeavyItem	-> ComedorMesa
 has	concealed female supporter transparent,
 with	short_name [;
			print "[](mesa) de comedor";
			return true;
		],
		name_f 'mesa',
		gender G_FEMENINO,
		adjectives 'comedor' 'ebano' 'negra',
		!!------------------
		description [;
			print "La mesa, construida en ébano negro, tiene al menos siete varas de largo. Dos personas sentadas a sus extremos prácticamente tendrían que gritar para hablar entre ellas.";
			if (child(self)) {
				new_line;
				new_line;
				print "Puedes ver [lista de objetos sobre self] sobre la [self](mesa:OTHER_OBJECT).";
			}
			new_line;
			return true;
		],
;

!! @Papel
Item	-> -> Papel
 has	male,
 with	short_name [;
			print "trozo de [](papel:REMARKABLE_OBJECT)";
			return true;
		],
		name_f 'hoja',
		name_m 'papel',
		gender G_MASCULINO,
		adjectives 'trozo',
		!!------------------
		before [;
			Read:
				print "*Esta noche otra vez. No se va a acabar nunca, ¿verdad? ¿No podré nunca ser libre y feliz?… Incluso si se terminase alguna vez, ¿podré quit-* (tachado) *deshacerme algún día del miedo y de las pesadillas? Lo odio. Me odio. Odio mi vida.* (incomprensible) *Su deseo es fuego que convierte la alegría en cenizas…*";
				new_line;
				return true;
		],
		description [;
			print "Arrugada y sucia, la hoja de papel lleva escritas varias frases en tinta negra y con la cuidadosa caligrafía de una muchacha";
			if (self has described) {
				print ". ";
			} else {
				print ":^^";
				<Read self>;
				new_line;
			}
			print "Tiene exactamente el mismo tamaño que las páginas del diario de María.";
			new_line;
			return true;
		],
;

!! @Duquesa
Item	Duquesa Limbo
 has	female proper,
 with	short_name [;
			print "cuerpo de [](Duquesa)";
			return true;
		],
		name_f 'duquesa' 'gata' 'gatita',
		name_m 'animal' 'cadaver' 'cuerpo' 'gato',
		gender G_FEMENINO,
		adjectives 'congelada' 'congeado' 'muerta' 'muerto',
		!!------------------
		description [;
			print "Se trata sin ninguna duda [del self], tu gata. Recuerdas el desconsuelo de María cuando Duquesa murió, hace años. Los conejos que la trajiste a casa después nunca la gustaron igual.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	4)	Torre del homenaje
!!
!!------------------------------------------------------------------------------

!! @DirTorre
AdjacentRoom	DirTorre
 has	male,
 with	found_in PatioArmas,
		short_name [;
			print "tramo de [](escalones:DIRECTION_OBJECT)";
			return true;
		],
		name_f 'escalera',
		name_fp 'escaleras',
		name_m 'escalon',
		name_mp 'escalones',
		gender G_MASCULINO,
		adjectives 'breve' 'corto' 'tramo',
		!!------------------
		adjacent_room Torre,
		before [;
			Enter:
				if (location == PatioArmas) {
					print "Subes el breve tramo de escaleras y entras en la torre del homenaje.";
					new_line;
				}
				self.AdjacentRoom::before();
				return true;
		],
;

!! @Torre
Indoors	Torre "Al pie de la torre principal"
 has	male,
 with	name_f 'torre',
		gender G_MASCULINO,
		adjectives 'homenaje' 'principal',
		!!------------------
		b_to [;
			<<Enter DirPatioArmas>>;
		],
		description [;
			print "Grandes fajos de papel abarrotan la sala circular. Amontonados unos sobre otros en innumerables pilas de diferente altura; anudados escrupulosamente unos y sueltos y sin orden aparente otros, bien ajustados, tambaleantes o caídos y desperdigados por el suelo. Ocupan prácticamente toda la habitación, dejando apenas el paso justo entre la salida al [DirPatioArmas](patio) y la escalera de caracol que asciende hasta las plantas superiores de la torre.";
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirCamara>>;
		],
;

!! @DirCamara
AdjacentRoom	-> DirCamara
 has	female
 with	short_name [;
			print "[](escalera:DIRECTION_OBJECT) de caracol";
			return true;
		],
		name_f 'escalera',
		name_fp 'escaleras',
		name_m 'escalon',
		name_mp 'escalones',
		gender G_FEMENINO,
		adjectives 'caracol',
		!!------------------
		adjacent_room Camara,
		before [;
			Enter:
				self.AdjacentRoom::before();
				return true;
		],
;


!!==============================================================================
!!
!!	5) Sótano
!!
!!------------------------------------------------------------------------------

!! @DirSotano
AdjacentRoom	DirSotano "sotano"
 has	male,
 with	found_in GrutaPasadizo,
		name_f 'antesala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!------------------
		adjacent_room GrutaVestibulo,
		before [;
			Enter:
				self.AdjacentRoom::before();
				return true;
		],
;

!! @Sotano
Indoors	Sotano "Sótano"
 has	male,
 with	name_f 'bodega',
		name_m 'sotano',
		gender G_MASCULINO,
		adjectives 'humeda' 'humedo',
		!!------------------
;
