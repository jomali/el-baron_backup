

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena cero (objetos comunes)
!!
!!==============================================================================
!!
!!	Archivo:		baron_scene-zero.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2019/06/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2019, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	1)	En algún lugar…
!!
!!------------------------------------------------------------------------------

!! @Limbo
Room	Limbo "En algún lugar…"
 has	male,
 with	name_f	'babia' 'parte',
		name_m	'limbo' 'lugar' 'sueno' 'vacio',
		gender	G_MASCULINO,
		adjectives 'algun' 'alguna',
		!!------------------
		description [;
			print "Más allá de la barrera casi insuperable que separa la realidad de todo lo demás. Un lugar etéreo, donde la vida, la materia y la vitalidad no son necesariamente constantes; un lugar donde el tiempo y el espacio no existen tal y como los capta nuestro ser despierto…";
			if (self hasnt visited) {
				print " *bienvenido al mundo del sueño*.";
			}
			new_line;
			return true;
		],
;

!! @Baron
Character	-> Baron "protagonista"
 has	male proper,
 with	name_m 'personaje' 'protagonista',
		gender G_MASCULINO,
		!!------------------
		daemon [;
			if (self.is_burning()) {
				if (self.burning_counter == 2) {
					new_line;
					print "[El Fuego] abrasa[n Fuego] tu cuerpo. Ves cómo el metal de tu [Armadura](armadura) empieza a tornarse rojo y se va volviéndo más brillante a cada momento que pasa.";
					new_line;
				} else if (self.burning_counter == 3) {
					give E1_ElHombreArdiente on;
					new_line;
					print "Golpeas las [Fuego](llamas) desesperadamente intentando apagarlas. Pero es inútil. Peor aún, observas con espanto que el metal de la [Armadura](armadura) ha comenzado a derretirse. Todas tus terminaciones nerviosas parecen estar aullando por el dolor.";
					new_line;
				} else if (self.burning_counter >= 4) {
					new_line;
					print "El metal fundido penetra a través de tu piel y se mezcla con el grasa y el músculo licuados. No ves nada ya. No oyes nada. No piensas en nada. La experiencia del dolor es lo único que ocupa los últimos vestigios de tu consciencia. Y ni siquiera eso. *Dolor* es una palabra que no significa nada. *Dolor* no se acerca a describir este tormento. Caes de rodillas tratando de gritar por última vez mientras tus cuerdas vocales y tus pulmones arden. La oscuridad te cubre como una manta fría.";
					new_line;
					return EndSceneOne();
				}
				self.burning_counter++;
			}
			return true;
		],
		is_blind [;
			return self.blind;
		],
		is_burning [;
			return self.burning;
		],
		is_naked [
			n_clothes obj;
			n_clothes = 0;
			objectloop(obj in self && obj has clothing && obj has worn) {
				n_clothes++;
			}
			return (n_clothes == 0);
		],
		is_running_away [;
			return self.running_away;
		],
		!!------------------
		blind false,
		burning false,
		running_away false,
 private
		burning_counter 0,
;

!! @Cielo
Atrezzo	Cielo "cielo"
 has	male,
 with	found_in [;
			if (location ofclass Outdoors) {
				return true;
			}
		],
		name_f 'estrella',
		name_fp 'estrellas',
		name_m 'cielo' 'firmamento',
		gender G_MASCULINO,
		adjectives 'despejado' 'nocturno',
		!!------------------
		description [;
			print "En la distancia cósmica las almas de los mayores guerreros del pasado arden salvajemente en eterna lucha con fuerzas desconocidas del más allá. Aquí, en la tierra, su luz lejana adorna el firmamento como hermosas velas contra terciopelo negro.";
			new_line;
			return true;
		],
;

!! @Luna
Atrezzo	Luna "luna"
 has	female light,
 with	found_in [;
		   if (location ofclass Outdoors) {
			   return true;
		   }
		],
		name_f 'luna',
		gender G_FEMENINO,
		!!------------------
		description [;
			print "La funesta luz de la luna desdibuja los contornos de los objetos; parecen fluir entre sí, intangibles, como elementos de un sueño.";
			new_line;
			return true;
		],
;

!! @Nieve
Atrezzo	Nieve "nieve"
 has	female,
 with	found_in [;
			if (location ofclass Outdoors) {
				return true;
			}
		],
		name_f 'nieve',
		gender G_FEMENINO,
		!!------------------
		before [;
			!! TODO - Hurgar con una herramienta
			Dig,
			LookUnder,
			Search:
				print "Hurgas aquí y allá entre [la self], sin encontrar nada especial.";
				new_line;
				return true;
		],
		description [;
			print "A veces la nieve otorga a los objetos una cualidad de cuento de hadas al posarse sobre ellos. Hoy no. No esta nieve. Esta nieve parece querer asfixiarlos y enterrarlos para siempre.";
			new_line;
			return true;
		],
;
