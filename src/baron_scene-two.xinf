

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena segunda
!!
!!==============================================================================
!!
!!	Archivo:		baron_scene-two.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2019/02/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2019, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	0)
!!
!!------------------------------------------------------------------------------

!! @Abrigo
Item	Abrigo "abrigo" Limbo
 class	Clothes,
 has	male,
 with	name_f 'pelliza',
		name_m 'abrigo' 'gaban' 'sobretodo',
		gender G_MASCULINO,
		adjectives 'invernal' 'invierno',
		!!------------------
		before [;
			Disrobe:
			!! TODO
				if (location ofclass Outdoors) {
					print "Hace un frío helador. El abrigo te protege de acabar congelado.";
					new_line;
					return true;
				}
				return false;
		],
		description [;
			print "Compraste este abrigo hace poco, obligado por la insistencia de tu mujer. Es verdad que el viejo estaba ya agujereado en algunas partes y que no protegía tan bien frío, pero estabas acostumbrado a él. Era *tu* abrigo. Las mujeres no comprenden estas cosas.";
			new_line;
			return true;
		],
;

!!==============================================================================
!!
!!	1)	En tu dormitorio
!!
!!------------------------------------------------------------------------------

!! @DirDormitorioBaron
AdjacentRoom	DirDormitorioBaron "dormitorio"
 has	male,
 with	found_in Descansillo,
		short_name [;
			print "[](dormitorio:DIRECTION_OBJECT) principal";
			return true;
		],
		name_f 'habitacion',
		name_m 'cuarto' 'dormitorio',
		gender G_MASCULINO,
		adjectives 'principal',
		!!------------------
		adjacent_room DormitorioBaron,
		before [;
			Enter:
				print "Regresas al dormitorio en completo silencio.";
				new_line;
				self.AdjacentRoom::before();
				return true;
		],
;

!! @DormitorioBaron
Indoors	DormitorioBaron
 has	male,
 with	short_name [;
			print "En tu [](dormitorio)";
			return true;
		],
		name_f	'estancia' 'habitacion',
		name_m	'cuarto' 'dormitorio',
		gender	G_MASCULINO,
		!!------------------
		description [;
			print "La luz de la luna se cuela a través de la ventana y arroja sobre los muebles una pátina nacarada de una belleza sobrenatural. Sólo la suave respiración de tu [Gilda](esposa) quiebra el silencio del cuarto, dormida";
			if (Gilda.is_faking()) {
				print " —o simulando hacerlo—";
			}
			print " en [la CamaMatrimonio]. Junto a un pesado armario de roble hay una puerta que conduce hasta [el DirDescansillo] de fuera.";
			new_line;
			return true;
		],
		exits [;
			print "Hay una única [DormitorioBaronPuerta](puerta) en la habitacion.";
			new_line;
			return true;
		],
		initial [;
			if (self hasnt visited) {
				new_line;
				print "Un tono estridente y discorde rompe dos veces el silencio de la noche, despertándote de un sueño intranquilo. Durante unos instantes aún puedes escuchar cómo el segundo tañido de la campana de la iglesia resuena en el aire. Para cuando el sonido desaparece por completo y la quietud de la noche se asienta sobre el pueblo una vez más, el recuerdo del sueño se ha desvanecido con él, aunque dudas que se tratase de algo que hubieses querido conservar, en cualquier caso. Ojalá fuese posible librarse de las demás pesadillas con esa misma facilidad…
				^^
				Es la hora. Te frotas los párpados con una mano y, poniendo sumo cuidado para no despertar a tu mujer, sales de la cama. Sin duda Gilda intentaría despertarte.
				^^";
				new_line;
				return true;
			}
		],
;

!! @CamaMatrimonio
HeavyItem	-> CamaMatrimonio "cama"
 has	concealed female supporter,
 with	short_name [;
			print "[](cama) de matrimonio";
			return true;
		],
		name_f 'cama' 'manta' 'piltra' 'sabana',
		name_fp 'mantas' 'sabanas',
		name_m
			'camastro' 'catre' 'colchon' 'jergon' 'lecho' 'mobiliario'
			'moblaje' 'mueble' 'somier',
		name_mp 'muebles',
		gender G_FEMENINO,
		adjectives 'matrimonio',
		!!------------------
		description [;
			print "Hecha con tus propias manos, ocupa casi la mitad de la habitación. Bajo la luz de la luna puedes ver cómo las mantas suben y bajan acompasadamente con la respiración de [Gilda].";
			new_line;
			return true;
		],
;

Atrezzo	-> -> "almohada"
 has	female,
 with	name_f 'almohada',
		name_fp 'almohadas',
		name_m 'almohadon' 'cojin',
		name_mp 'almohadones' 'cojines',
		gender G_FEMENINO,
		adjectives 'pluma' 'plumas' 'plumon',
		!!------------------
		description [;
			print "Una larga almohada rellena de plumas.";
			new_line;
			return true;
		],
;

!! @Gilda
Character	-> -> Gilda "Gilda"
 has	female proper,
 with	name_f 'mujer',
		gender G_FEMENINO,
		!!------------------
		description [;
			if (self hasnt described) self.state++;
			return false;
		],
		is_faking [;
			return (self.state > 0);
		],
;

!! @DormitorioBaronMesita
Item	-> DormitorioBaronMesita
 has	female,
 with	short_name [;
			print "[](mesita:REMARKABLE_OBJECT) de noche";
			return true;
		],
		name_f 'mesita',
		gender G_FEMENINO,
		adjectives 'noche',
		!!------------------
		invent [;
			if (inventory_stage == 1) {
				print "[una self], al lado de la [CamaMatrimonio](cama:OTHER_OBJECT)";
				return true;
			}
		],
;

!! @DormitorioBaronPuerta
Item	-> DormitorioBaronPuerta
 has	female ~open openable scenery,
 with	short_name [;
			print "[](puerta) al descansillo";
			return true;
		],
		name_f 'entrada' 'puerta' 'salida',
		gender G_FEMENINO,
		adjectives 'estrecha',
		!!------------------
		after [;
			Close:
				print "Cierras la puerta con delicadeza.";
				new_line;
				return true;
			Open:
				print "Abres la puerta cuidando no hacer ruido.";
				new_line;
				return true;
		],
		before [;
			Enter:
				<<Enter DirDescansillo>>;
		],
		description [;
			print "La estrecha puerta separa tu dormitorio del [DirDescansillo](rellano). Está";
			if (self has open) {
				print "abierta";
			} else {
				print "cerrada";
			}
			print ".";
			new_line;
			return true;
		],
;

!! @DormitorioBaronSilla
Item	-> DormitorioBaronSilla
 has	female supporter,
 with	short_name [;
			print "[](silla:OTHER_OBJECT)";
			return true;
		],
		name_f 'silla',
		gender G_FEMENINO,
		!!------------------
		after [
			i obj;
			LetGo,
			Receive:
				objectloop(obj in self && obj hasnt scenery
					&& obj hasnt concealed) {
					i++;
				}
				if (i > 0) {
					give self ~concealed;
				} else {
					give self concealed;
				}
		],
		describe [;
			if (IndirectlyContains(self, Ropa)) {
				new_line;
				print "Sobre [la self] descansa [una Ropa].";
				new_line;
				return true;
			}
		],
;

!! @Ropa
Item	-> -> Ropa
 class	Clothes
 has	female,
 with	short_name [;
			print "[](ropa:REMARKABLE_OBJECT) de trabajo";
			return true;
		],
		name_f 'indumentaria' 'prenda' 'ropa' 'vestimenta',
		name_fp 'prendas' 'vestimentas',
		name_m 'vestuario',
		name_mp 'atavios' 'atuendos',
		gender G_FEMENINO,
		adjectives 'trabajo',
		article "tu",
		!!------------------
;

!! @DormitorioBaronVentana
Atrezzo	-> DormitorioBaronVentana "ventana"
 has	female,
 with	name_f 'ventana',
		gender G_FEMENINO,
		!!------------------
;


!!==============================================================================
!!
!!	2)	Descansillo
!!
!!------------------------------------------------------------------------------

!! @DirDescansillo
AdjacentRoom	DirDescansillo "descansillo"
 has	male,
 with	found_in DormitorioBaron DormitorioMaria,
		name_m 'descansillo' 'rellano',
		gender G_MASCULINO,
		!!------------------
		adjacent_room Descansillo,
		before [;
			Enter:
				if (location == DormitorioBaron
					&& DormitorioBaronPuerta hasnt open) {
					<Open DormitorioBaronPuerta>;
				}
				if (location == DormitorioMaria
					&& DormitorioMariaPuerta hasnt open) {
					<Open DormitorioMariaPuerta>;
				}
				self.AdjacentRoom::before();
				return true;
		],
;

!! @Descansillo
Indoors	Descansillo "Descansillo"
 has	male,
 with	name_m 'descansillo' 'rellano',
		gender G_MASCULINO,
		!!------------------
		description [;
			print "Eres capaz de distinguir sin dificultad las familiares formas y los objetos que te rodean, a pesar de que las sombras parecen envolver el descansillo como una mortaja. Gilda descansa en el [DirDormitorioBaron] principal";
			!! TODO
			print ", a tu espalda";
			print ", mientras que";
			print " frente a ti";
			print " está [la DirDormitorioMaria] del cuarto de María. [Unas Escaleras] descienden hasta la planta baja.";
			new_line;
			return true;
		],
;

!! @Escaleras
AdjacentRoom	-> Escaleras "escaleras"
 has	female pluralname,
 with	name_f 'escalera',
		name_fp 'escaleras',
		name_m 'escalon',
		name_mp 'escalones',
		gender G_FEMENINO + G_PLURAL,
		!!------------------
		adjacent_room Jardin,
		before [;
			Enter:
				move Abrigo to player;
				give Abrigo worn;
				print "Desciendes a tientas a través de la oscuridad. En el recibidor recoges tu abrigo y te lo colocas antes de abrir la puerta de la calle y salir al frío de la noche.";
				new_line;
				new_line;
				self.AdjacentRoom::before();
				return true;
		],
		description [;
			print "Las escaleras de madera descienden en círculo a las profundidades negras como la tinta.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	3)	Habitación de María
!!
!!------------------------------------------------------------------------------

!! @DirDormitorioMaria
AdjacentRoom	DirDormitorioMaria
 has	female,
 with	found_in Descansillo,
		short_name [;
			print "[](puerta:DIRECTION_OBJECT) roja";
			return true;
		],
		name_f 'entrada' 'puerta',
		gender G_FEMENINO,
		adjectives 'de' 'maria' 'roja',
		!!------------------
		adjacent_room DormitorioMaria,
		before [;
			Enter:
				if (DormitorioMariaPuerta hasnt open) {
					<Open DormitorioMariaPuerta>;
				}
				self.AdjacentRoom::before();
				return true;
		],
;

!! @DormitorioMaria
Indoors	DormitorioMaria "Habitación de Maria"
 has	female,
 with	name_f 'estancia' 'habitacion',
		name_m 'cuarto' 'dormitorio',
		gender G_FEMENINO,
		adjectives 'maria',
		!!------------------
		description [;
			print "Sin la presencia radiante de María su cuarto parece vacío y muerto. Es un espectáculo deprimente: una cama en la que no descansa nadie ya, un escritorio tras el que nadie se sienta a estudiar y hacer dibujos, un armario que guarda ropa que nadie reclama, una casa de muñecas con la que no hay nadie que juegue. Los vivos colores con los que pintaste el techo, los muebles y la puerta [al DirDescansillo] parecen una paleta de grises en la luz pálida de las estrellas.";
			new_line;
			return true;
		],
;

!! @DormitorioMariaPuerta
Item	-> DormitorioMariaPuerta "puerta"
 has	female open openable scenery,
 with	name_f 'entrada' 'puerta' 'salida',
		gender G_FEMENINO,
		adjectives 'roja',
		!!------------------
		before [;
			Enter:
				<<Enter DirDescansillo>>;
		],
		description [;
			if (location == DormitorioMaria) {
				print "La puerta conduce de vuelta al rellano.";
			} else {
				print "Pintada en un alegre color rojo, esta puerta conduce a la habitación de tu hija raptada.";
			}
			new_line;
			return true;
		],
;

!!==============================================================================
!!
!!	4)	Frente a tu casa
!!
!!------------------------------------------------------------------------------

!! @Jardin
Outdoors	Jardin "Frente a tu casa"
 has	male,
 with	name_m 'jardin',
		gender G_MASCULINO,
		adjectives 'exterior',
		!!------------------
		description [;
			print "Un grueso manto de nieve cubre el jardín, perturbado únicamente por algunos pocos arbustos raquíticos y sin hojas que sobresalen aquí y allá junto a la valla de madera. Cruzando el jardín puedes ver el camino que parte de la plaza mayor del pueblo y serpentea, por entre edificios primero y por entre los campos de labranza después, en dirección al bosque. Y [al JardinDirCastillo] del barón.";
			new_line;
			return true;
		],
;

!! @JardinDirCastillo
AdjacentRoom	-> JardinDirCastillo "castillo"
 has	male,
 with	name_m 'castillo',
		gender G_MASCULINO,
		adjectives 'baron',
		!!------------------
		adjacent_room CaminoBosque1,
		before [;
			Enter:
				if (self.state == 0) {
					self.state++;
					print "Te dispones a emprender el camino hacia el castillo del barón pero antes de echar a andar, vacilas. La ira ha ido creciendo poco a poco en tu interior al pensar en tus vecinos, que parecen dormir apaciblemente en el calor de sus casas. ¿Cómo pueden descansar tranquilos? ¿Cómo es posible que ni uno sólo haya tenido agallas suficientes para levantar la voz y ayudar a socorrer a María?
					^^
					Faltos de empatía… tan individuales… tan asquerosamente egoístas. Todos saben lo que ha ocurrido pero nadie piensa mover un músculo para remediarlo porque nadie se siente responsable. Porque es más cómodo ignorar lo que sucede a tu alrededor mientras no te afecte directamente…";
					new_line;
					return true;
				}
				print "Mascullas un insulto contra la humanidad en su conjunto y te abres paso a grandes zancadas por entre la capa de nieve.
				^^";
				new_line;
				Utils.print_horizontal_rule();
				new_line;
				self.AdjacentRoom::before();
				return true;
		],
;
