

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena primera
!!
!!==============================================================================
!!
!!	Archivo:		baron_escena-uno.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2019/02/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2018, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	0)
!!
!!------------------------------------------------------------------------------

[ EndSceneOne;
	!! Actualizaciones de estado:
	StopDaemon(Baron);
	StopDaemon(Dragon);
	Baron.is_burning = false;

	new_line;
	new_line;
	print (parser) "Pulsa una tecla.";
	Utils.press_any_key();
	Utils.erase_window();
	new_line;
	VersionSub();
	new_line;
	print (important) "Fin de la primera escena.", "^";
	print "Aquí concluye la demostración.^";
	new_line;
	print (parser) "Pulsa una tecla para salir.";
	new_line;

	quit;

	!! Texto de transición:
	!! previous_style = Utils.set_text_style(TEXT_STYLE_HEADER);
	!! print (string) Story;
	!! Utils.set_text_style(previous_style);
	!! print "^^";
	!! print "Una historia interactiva de Victor Gijsbers.^";
	!! print "Edición de J. Francisco Martín^";
	!!
	!! return true;
];

!! @Tutorial
Object	Tutorial "(Tutorial)"
 has	scenery,
 with	actions_hint [
			previous_style;
			if (self.actions_hint_printed) return true;
			self.actions_hint_printed = true;
			new_line;
			previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
			print (string) TEXT_STYLE_PARSER_PREFIX;
			print "Tutorial: Puedes realizar acciones escribiendo un verbo en imperativo junto a una palabra destacada asociada a un objeto. Puedes intentar, por ejemplo, ver las diferencias entre ";
			if (self.hyperlinks_status()) print "hacer ~clic~ sobre";
			else print "teclear";
			print " PUERTAS, y teclear ABRE PUERTAS o LLAMA A LAS PUERTAS.";
			print (string) TEXT_STYLE_PARSER_SUFIX;
			Utils.set_text_style(previous_style);
			new_line;
			return true;
		],
		hesitations_hint [
			previous_style;
			if (self.hesitations_hint_printed) return true;
			self.hesitations_hint_printed = true;
			new_line;
			previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
			print (string) TEXT_STYLE_PARSER_PREFIX;
			print "Tutorial: Cuando el protagonista da muestras de duda o indecisión suele significar que estás a punto de iniciar una acción con consecuencias irreversibles. Si de verdad quieres seguir adelante, simplemente vuelve a ";
			if (self.hyperlinks_status()) print "utilizar";
			else print "teclear";
			print " la misma acción.";
			print (string) TEXT_STYLE_PARSER_SUFIX;
			Utils.set_text_style(previous_style);
			new_line;
			return true;
		],
		keywords_hint_a [
			previous_style;
			if (self.keywords_hint_a_printed) return true;
			self.keywords_hint_a_printed = true;
			new_line;
			previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
			print (string) TEXT_STYLE_PARSER_PREFIX;
			print "Tutorial: ";
			if (self.hyperlinks_status()) print "Haz ~clic~ sobre";
			else print "Teclea";
			print " las palabras destacadas que vayas encontrando, como OSCURIDAD o RESPLANDOR, para hacer avanzar la historia. Teclea ";
			Utils.set_text_style(previous_style);
			PRT__("resaltado", 0, 2);
			Utils.set_text_style(TEXT_STYLE_PARSER);
			print " si estas palabras no aparecían con un estilo destacado en el párrafo anterior (o si prefieres modificar su aspecto).";
			print (string) TEXT_STYLE_PARSER_SUFIX;
			Utils.set_text_style(previous_style);
			new_line;
			return true;
		],
		keywords_hint_b [
			previous_style;
			if (self.keywords_hint_b_printed) return true;
			self.keywords_hint_b_printed = true;
			new_line;
			previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
			print (string) TEXT_STYLE_PARSER_PREFIX;
			print "Tutorial: Aquellos ";
			Utils.set_text_style(previous_style);
			PRT__("objetos", 0, 2);
			Utils.set_text_style(TEXT_STYLE_PARSER);
			print " con los que puedas interactuar aparecerán destacados con este estilo de texto. En general, al ";
			if (self.hyperlinks_status()) print "hacer ~clic~ sobre";
			else print "teclear";
			print " el nombre de estos objetos el protagonista los examinará y podrás conseguir más información sobre ellos.";
			print (string) TEXT_STYLE_PARSER_SUFIX;
			Utils.set_text_style(previous_style);
			new_line;
			return true;
		],
		keywords_hint_c [
			previous_style;
			if (self.keywords_hint_c_printed) return true;
			self.keywords_hint_c_printed = true;
			new_line;
			previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
			print (string) TEXT_STYLE_PARSER_PREFIX;
			print "Tutorial: Hay un segundo tipo de palabra destacada relacionado con las ";
			Utils.set_text_style(previous_style);
			PRT__("salidas", 0, 3);
			Utils.set_text_style(TEXT_STYLE_PARSER);
			print ". Puedes ";
			if (self.hyperlinks_status()) print "hacer ~clic~ sobre";
			else print "teclear";
			print " el nombre de una salida para desplazarte de una localidad a otra.";
			print (string) TEXT_STYLE_PARSER_SUFIX;
			Utils.set_text_style(previous_style);
			new_line;
			return true;
		],
		template [
			previous_style;
			previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
			print (string) TEXT_STYLE_PARSER_PREFIX;
			print (string) TEXT_STYLE_PARSER_SUFIX;
			Utils.set_text_style(previous_style);
			new_line;
			return true;
		],
 private
		hyperlinks_status [;
			if (Utils.get_hyperlinks_status(1)
				&& Utils.get_hyperlinks_status(2)
				&& Utils.get_hyperlinks_status(3)) {
				return true;
			} else {
				return false;
			}
		],
		actions_hint_printed false,
		hesitations_hint_printed false,
		keywords_hint_a_printed false,
		keywords_hint_b_printed false,
		keywords_hint_c_printed false,
;

!! @Azufre
Immaterial	"azufre"
 has	male,
 with	found_in GrutaDragon GrutaPasadizo GrutaVestibulo,
		name_m 'azufre' 'sulfuro',
		gender G_MASCULINO,
		before [;
			DefaultAction,
			Smell:
				<<Smell>>;
		],
;

!! @Calor
Immaterial	"calor"
 has	male,
 with	found_in GrutaDragon GrutaPasadizo GrutaVestibulo,
		name_f 'calidez' 'temperatura',
		name_m 'ardor' 'calor',
		gender G_MASCULINO,
		adjectives 'alta' 'alto' 'elevada' 'elevado' 'gran' 'grande',
		before [;
			DefaultAction,
			Examine:
				if (location == GrutaVestibulo) {
					print "La temperatura aquí dentro es mucho mayor que en el exterior.";
				} else {
					print "Cuanto más te internas en la cueva mayor es el calor.";
				}
				new_line;
				return true;
		],
;

!! @Ruido
Immaterial	"ruido"
 has	male,
 with	found_in GrutaDragon GrutaPasadizo GrutaVestibulo,
		name_m 'grunido' 'murmullo' 'rugido' 'ruido',
		gender G_MASCULINO,
		before [;
			DefaultAction,
			Listen:
				<<Listen>>;
		],
;

!! @DragonDistancia
Item	"dragón"
 has	male scenery,
 with	found_in GrutaPasadizo GrutaVestibulo,
		name_m 'dragon' 'monstruo',
		gender G_MASCULINO,
		description [;
			print "El dragón habita en el interior de la gruta[if:compass_mode], hacia [el n_obj][fi].";
			new_line;
			return true;
		],
		before [;
			DefaultAction,
			Examine:
				return false;
			default:
				print "Tienes que llegar antes hasta su guarida.";
				new_line;
				return true;
		],
;

!! @Oscuridad
!! Array darknessFlags -> 2; ! hasta (N-1) * 8 flags
Atrezzo	Oscuridad "oscuridad"
 has	female,
 with	found_in GrutaDragon GrutaPasadizo GrutaVestibulo,
		name_f 'oscuridad' 'sombra' 'tiniebla',
		name_fp 'sombras' 'tinieblas',
		gender G_FEMENINO,
		description [;
			if (GrutaVestibulo.brighter(self)) return true;
			print "Las sombras titilan con la luz y dibujan formas macabras entre [los Huesos] del suelo.";
			new_line;
			return true;
		],
		!! each_turn [;
		!! 	if (location == GrutaVestibulo
		!! 		&& GrutaVestibulo.state == 3
		!! 		&& random(5) == 1) {
		!! 		new_line;
		!! 		switch (Shuffle(2, darknessFlags)) {
		!! 			1:
		!! 				print "Las sombras tiemblan con la débil luz que entra desde el pasadizo.";
		!! 			2:
		!! 				print "Los huesos parecen moverse como alimañas entre tus pies, pero no es más que un efecto óptico producido por las sombras titilantes.";
		!! 		}
		!! 		new_line;
		!! 		return true;
		!! 	}
		!! ],
;

!! @Fuego
Atrezzo	Fuego "resplandor"
 has	male,
 with	found_in GrutaDragon GrutaPasadizo GrutaVestibulo,
		name_f 'claridad' 'llama' 'luz',
		name_fp 'llamas',
		name_m 'fuego' 'fulgor' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		description [;
			if (GrutaVestibulo.brighter(self)) {
				return true;
			} else if (Baron.is_blind) {
				print "Un cálido fulgor que late en las profundidades de la cueva.";
			} else if (Baron.is_burning) {
				print "El destello de un mar de fuego lo ocupa todo a tu alrededor.";
			} else {
				switch (real_location) {
					GrutaVestibulo:
						print "Ves un baile de llamas en la distancia. Su calor se puede sentir claramente incluso desde aquí.";
					GrutaPasadizo:
						print "Lenguas de fuego mayores que cualquier hombre iluminan el pasadizo desde la [DirGrutaDragon](guarida) del dragón.";
					GrutaDragon:
						print "Llamas gigantescas brotan del suelo hayá dónde mires.";
				}
			}
			new_line;
			return true;
		],
		before [;
			Blow:
				if (Baron.is_burning) {
					print "Intentas aplacar las llamas en vano; parecen mayores y más abrasadoras a cada momento.";
				} else {
					print "Nadie podría apagar un fuego como ese.";
				}
				new_line;
				return true;
			Drop:
				if (IndirectlyContains(player, self)) {
					print "El fuego se niega a soltarte a ti.";
					new_line;
					return true;
				}
				return false;
			Enter:
				if (real_location == GrutaVestibulo) {
					<<Enter DirGrutaPasadizo>>;
				}
			self.Atrezzo::before();
		],
;


!!==============================================================================
!!
!!	1)	Vestíbulo de la gruta
!!
!!------------------------------------------------------------------------------

!! @DirGrutaVestibulo
AdjacentRoom	DirGrutaVestibulo "vestíbulo"
 has	male,
 with	found_in GrutaPasadizo,
		name_m 'hall' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresco' 'huesos' 'los' 'pequeno',
		adjacent_room GrutaVestibulo,
;

!! @GrutaVestibulo
Indoors	GrutaVestibulo "Vestíbulo de la gruta"
 has	male,
 with	name_f 'cueva' 'estancia' 'gruta' 'hall' 'sala',
		name_m 'salon' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'pequena' 'pequeno',
		!!----------------------------------------------------------------------
		before [;
			Listen:
				if (noun == 0) {
					if (Baron.is_blind) {
						print "Desde el interior de la gruta";
					} else {
						print "Desde el [DirGrutaPasadizo](pasadizo)";
					}
					print " se oye una reverberación grave y discontinua.";
					new_line;
					return true;
				}
			Smell:
				if (noun == 0) {
					print "El penetrante olor del azufre llena la cueva.";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		brighter [ obj;
			switch (self.state) {
				0:	!! Cegado completamente
					self.on_brighter(1);
					print "Poco a poco tus pupilas se van adaptando a la falta de luz y empiezas a distinguir formas entre las [Oscuridad](sombras:2) que te rodean. ";
					self.description();
					return true;
				1,
				2:	!! Cegado parcialmente
					if (obj == Fuego or Oscuridad) {
						self.on_brighter(3);
						print "Las [Oscuridad](tinieblas) siguen desvaneciéndose conforme tus ojos terminan de habituarse a la débil luz del vestíbulo y puedes distinguir de pronto las [Fuego](llamas) en la distancia. Sientes su calor. Un [Fuego](incendio) salvaje arde en las profundidades de la gruta y arroja destellos de luz rojiza entorno a ti. Miras a tu alrededor.";
						new_line;
						new_line;
						self.description();
						return true;
					}
					if (obj == 0) {
						self.on_brighter(3);
						print "Las formas se dibujan con mayor claridad a tu alrededor a medida que tus ojos se terminan de habituar a [la Oscuridad] de la gruta. ";
						self.description();
						return true;
					}
			}
			return false;
		],
		cant_go [;
			print "No puedes ir por ahí. ";
			return self.exits();
		],
		daemon [ flag;
			!! Los ojos del Barón se acostumbran automáticamente a la oscuridad
			!! de la gruta con el tiempo:
			if (self.counter < (self.limit_a + self.limit_b)) self.counter++;
			if (self.counter == self.limit_a) {
				if (~~flag) new_line;
				self.brighter(Oscuridad);
			}
			if (self.counter == (self.limit_a + self.limit_b)) {
				if (~~flag) new_line;
				self.brighter();
			}
			return true;
		],
		description [;
			if ((self.counter == (self.limit_a-1))
				|| (self.counter == (self.limit_a+self.limit_b-1))) {
				self.daemon(true);
				return true;
			}
			if (Baron.is_blind) {
				switch (self.state) {
					0:	!! Cegado completamente
						print "El interior de la gruta está sumido en la [Oscuridad](oscuridad:2), salvo por un [Fuego](resplandor:2) impreciso que parece surgir desde algún lugar a lo lejos.";
						if (self hasnt visited) {
							print " Olor a azufre. Un murmullo grave propagándose por las paredes. La temperatura ha aumentado ostensiblemente de pronto, como si acabases de atravesar el umbral de una forja.";
						}
						new_line;
						Tutorial.keywords_hint_a();
					1:	!! Cegado parcialmente (A)
						self.state = 2;
						print "Estás en una pequeña sala rectangular, débilmente iluminada por ese [Fuego](resplandor:2) anaranjado que parece proceder de algún punto en las profundidades de la gruta. A tus pies se extiende una alfombra de [Huesos](huesos:2) humanos.";
						new_line;
					2:	!! Cegado parcialmente (B)
						print "Estás en una pequeña sala rectangular, débilmente iluminada por ese [Fuego](resplandor:2) anaranjado que parece proceder de algún punto en las profundidades de la gruta. Los [Huesos](huesos:2) y [Huesos](cráneos) de aquellos que vinieron aquí antes que tú se extienden como una alfombra a tus pies.";
						new_line;
				}
			} else if (Baron.is_burning) {
				print "A través de las llamas distingues [Marmol](mármol:2) negro. [Huesos](Huesos:2) en el suelo. Las dos [PuertasGruta](puertas:2) de bronce que bloquean la única salida.";
				new_line;
			} else {
				print "Esta pequeña sala rectangular ha sido excavada cuidadosamente en la roca y sus paredes y su techo han sido recubiertos después con planchas de [Marmol](mármol:2) negro. Dos [PuertasGruta](puertas:2) de bronce sellan la salida[if:compass_mode] [al s_obj][fi] mientras que,[if:compass_mode] [al n_obj][else] al otro lado del vestíbulo[fi], un estrecho [DirGrutaPasadizo](pasadizo) conduce hasta la ardiente guarida del dragón. Lecho de [Huesos](huesos:2) y [Huesos](cráneos) ennegrecidos.";
				new_line;
				Tutorial.keywords_hint_c();
			}
			return true;
		],
		exits [;
			if (Baron.is_blind) {
				print "Las única salida visible son las [PuertasGruta](puertas) que tienes cerradas a tu espalda.";
			} else {
				print "Las únicas salidas visibles son las [PuertasGruta](puertas) cerradas y el estrecho [DirGrutaPasadizo](pasadizo) del otro lado del vestíbulo.";
			}
			new_line;
			return true;
		],
		in_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		initial [;
			if (self hasnt visited) {
				StartDaemon(self);
				Baron.is_blind = true;
				print "^^^^
				Las puertas de bronce se cierran a tu espalda y escuchas el chirrido de pesados cerrojos deslizándose en su interior. Está bien; te puedes encargar de esto tú solo. Con el hacha en alto das un paso adelante, hacia el calor.
				^^^^";
				Banner();
				new_line;
				return true;
			}
		],
		list_visible_objects [;
			if (self.state == 0) {
				new_line;
				print "Ni siquiera alcanzas a ver ";
				WriteListFrom(child(self),
					ENGLISH_BIT + WORKFLAG_BIT + RECURSE_BIT
					+ PARTINV_BIT + TERSE_BIT + CONCEAL_BIT);
				print ", aún estando a tus pies.";
				new_line;
				return true;
			} else if (self.state == 1 or 2) {
				new_line;
				print "También en el suelo puedes ver ";
				WriteListFrom(child(self),
					ENGLISH_BIT + WORKFLAG_BIT + RECURSE_BIT
					+ PARTINV_BIT + TERSE_BIT + CONCEAL_BIT);
				print ".";
				new_line;
				return true;
			}
			return false;
		],
		out_to [;
			PronounNotice(PuertasGruta);
			!SetPronoun('-las', PuertasGruta);
			return PuertasGruta;
		],
 private
		on_brighter [ next_state;
			switch (self.state) {
				0:
					self.state = next_state;
					self.counter = self.limit_a;
					move Huesos to self;
				1,
				2:
					StopDaemon(self);
					self.counter = (self.limit_a + self.limit_b + 1);
					self.state = next_state;
					Baron.is_blind = false;
					move Marmol to self;
					move GrabadoHombreArdiendo to PuertasGruta;
					give PuertasGruta ~described;
			}
		],
		counter 0,
		limit_a 3,
		limit_b 4,
;

!! @Baron
Character	-> Baron "Barón"
 has	male,
 with	name_m 'hombre' 'persona' 'personaje' 'protagonista',
		gender G_MASCULINO,
		daemon [;
			if (self.is_burning) {
				switch (self.burning_counter) {
					2:
						new_line;
						print "Las llamas te cubren por completo. El metal de tu armadura empieza a tornarse rojo, más brillante a cada momento.";
					3:
						new_line;
						print "El tormento es mayor a cada momento. Golpeas las llamas esperando apagarlas pero es inútil. Peor aún, observas con espanto que el metal de la armadura empieza a derretirse.";
					4:
						new_line;
						print "El metal líquido penetra a través de tu piel y se mezcal con la grasa y el músculo licuados. No ves nada. No oyes nada. No piensas en nada. Dolor. Ninguna otra cosa ocupa tu concienta, y tampoco eso. *Dolor* es una palabra que ha perdido el sentido. *Dolor* no se acerca a describir este sufrimiento. Caes de rodillas intentando gritar por última vez mientras tus cuerdas vocales y tus pulmones arden. La oscuridad cae sobre ti como una fría manta.";
						EndSceneOne();
						return true;
				}
				self.burning_counter++;
			}
		],
		held_item [
			obj;
			objectloop(obj in self) {
				if (obj hasnt concealed or scenery or worn) {
					return obj;
				}
			}
			return 0;
		],
		capacity_size 1,
		is_blind false,
		is_burning false,
		is_running_away false,
 private
		burning_counter 0,
;

!! @Armadura
Clothes	-> -> Armadura
 has	male worn,
 with	short_name [;
			print "conjunto completo de [](armadura)";
			return true;
		],
		name_f 'armadura' 'cota' 'malla',
		name_fp 'cintas' 'mallas' 'placas',
		gender G_MASCULINO,
		adjectives 'acero' 'completo' 'conjunto' 'cuero',
		description [;
			if (Baron.is_burning) {
				print "Incluso el acero de la armadura se está fundiendo en el calor del fuego. Gotas blancas de metal líquido que se abren paso a través de tu piel como cuchillos.";
			} else {
				if (real_location == GrutaPasadizo or GrutaDragon) {
					print "Estás sudando copiosamente pero jamás";
				} else {
					print "Jamás";
				}
				print " te enfrentarías al monstruo sin un conjunto completo de armadura. Gruesas placas de acero sobre cota de mallas, todo bien engrasado. Ofrece protección óptima contra cualquier tipo de ataque físico. El conjunto está ajustado por unas cintas de cuero, a la espalda.";
			}
			new_line;
			return true;
		],
		before [;
			Disrobe:
				if (Baron.is_burning) {
					print "El metal de la armadura te está abrasando. Desearías poder arrancarte la piel pero no puedes desprenderte de ella. Dolor como de un millón de cuchillos al rojo perforándote el cuerpo.";
				} else {
					print "No puedes quitarte la armadura solo; está asegurada con prietas cintas de cuero a la espalda. Una vez hayas derrotado al dragón podrás atravesar las puertas de bronce y tu escudero te ayudará a quitártela de encima.";
				}
				new_line;
				return true;
		],
		layer 0,
;

!! @Casco
Clothes	-> -> Casco "casco"
 has	male transparent worn,
 with	name_m 'casco' 'morrion' 'yelmo',
		gender G_MASCULINO,
		adjectives 'funcional',
		article "tu",
		description [;
			print "Un [self](casco) funcional, sin adornos de ningún tipo.";
			if (self in player && self has worn) {
				if (CascoVisera has open) {
					print " La [CascoVisera](visera:1) está abierta, dejando libre todo el campo visual pero exponiendo tu rostro ante un eventual ataque.";
				} else {
					print " La [CascoVisera](visera:1) está cerrada, ofreciendo la máxima protección.";
				}
			} else {
				if (CascoVisera has open) {
					print " Tiene la [CascoVisera](visera:1) abierta.";
				} else {
					print " Tiene la [CascoVisera](visera:1) cerrada.";
				}
			}
			new_line;
			return true;
		],
		before [;
			if (Baron.is_blind && self notin player) {
				print "Descansa entre las sombras del suelo.";
				new_line;
				return true;
			}
			Close:
				<<Close CascoVisera>>;
			Disrobe:
				if (self in player && self has worn) {
					give self ~worn;
					move self to parent(player);
					print "Te quitas el [self](casco:1) y lo posas en el suelo. Sin él te sientes totalmente vulnerable.";
					new_line;
					return true;
				}
			Drop:
				if (self in player && self has worn) {
					<<Disrobe self>>;
				}
			Open:
				<<Open CascoVisera>>;
			Wear:
				if (TestScope(self, player) && self notin player) {
					<<Take self>>;
				}
		],
		after [;
			Take:
				give self worn;
				print "L[o self] recoges del suelo y te l[o self] pones.";
				new_line;
				return true;
		],
;

!! @CascoVisera
!! TODO - Aceptar los adjetivos abierto/cerrado contextualmente (modificando
!! la rutina de parsing del propio objeto)
Item	-> -> -> CascoVisera "visera"
 has	female open openable,
 with	name_f 'visera',
		name_m 'visor',
		gender G_FEMENINO,
		adjectives 'casco',
		before [;
			Close:
				if (self hasnt open) {
					print "[El self] ya está cerrad[o self].";
					new_line;
					return true;
				} else {
					give self ~open;
					print "Cierras la visera del [Casco](casco:1).";
					new_line;
					return true;
				}
			DefaultAction:
				if (self has open) {
					<<Close self>>;
				} else {
					<<Open self>>;
				}
			Examine:
				<<Examine Casco>>;
			Open:
				if (self has open) {
					print "[El self] ya está abiert[o self].";
					new_line;
					return true;
				} else {
					give self open;
					print "Abres la visera del [Casco](casco:1).";
					new_line;
					return true;
				}
		],
;

!! @HachaGuerra
Item	-> -> HachaGuerra
 has	male,
 with	short_name [;
			print "[](hacha) de guerra";
			return true;
		],
		name_f 'arma' 'hacha',
		gender G_MASCULINO,
		article "tu",
		adjectives 'batalla' 'guerra',
		description [;
			print "Para esta gesta has traído el más pesado de tus hachas de guerra. No son muchos los que pueden levantar su peso y menos aún manejarlo con tu misma velocidad y precisión mortíferas.";
			new_line;
			return true;
		],
		before [;
			if (Baron.is_blind && self notin player) {
				print "Descansa entre las sombras del suelo.";
				new_line;
				return true;
			}
		],
;

!! @Huesos
Atrezzo	-> Huesos "huesos"
 has	male pluralname,
 with	name_f 'calavera',
		name_fp 'calaveras',
		name_m 'craneo' 'esqueleto' 'hueso',
		name_mp 'craneos' 'esqueletos' 'huesos',
		gender G_MASCULINO + G_PLURAL,
		adjectives 'depedazado' 'despedazados' 'ennegrecida' 'ennegrecidas'
			'ennegrecido' 'ennegrecidos' 'humana' 'humanas' 'humano' 'humanos'
			'negra' 'negras' 'negro' 'negros',
		description [;
			print "El suelo está cubierto de esqueletos despedazados. Negros por el fuego que en algún momento consumió la carne de sus cuerpos.";
			new_line;
			return true;
		],
		before [;
			if (GrutaVestibulo.state == 0) {
				print "No puedes ver nada parecido entre las sombras.";
				new_line;
				return true;
			}
			Take:
				print "No quieres cargar con recuerdos del fracaso de aquellos que vinieron antes que tú.";
		],
;

!! @Marmol
Atrezzo	-> Marmol "mármol"
 has	female pluralname transparent,
 with	name_f 'pared',
		name_fp 'paredes',
		name_m 'marmol',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'negra' 'negras' 'negro' 'planchas' 'pulida' 'pulidas'
			'pulido',
		description [;
			print "Tanto las paredes como el techo están cubiertos por unas placas de mármol negro muy pulido. Vetas blancas formando intrincados [PatronesMarmol] sobre su superficie.";
			new_line;
			return true;
		],
		before [;
			if (Baron.is_blind) {
				print "No puedes ver nada parecido entre las sombras.";
				new_line;
				return true;
			}
		],
;

!! @PatronesMarmol
Atrezzo	-> -> PatronesMarmol "patrones"
 has	male pluralname,
 with	name_f 'estria' 'linea' 'raya' 'veta',
		name_fp 'estrias' 'lineas' 'rayas' 'vetas',
		name_m 'patron',
		name_mp 'patrones',
		gender G_MASCULINO + G_PLURAL,
		adjectives 'blanca' 'blancas' 'blanco' 'blancos' 'marmol',
		description [;
			if (self hasnt described) {
				print "Al estudiar los patrones se apodera de ti el presentimiento de que, de algún modo, esas estrías blancas pueden esconder un significado oculto. Que bajo su aparente caos quizá se haye la respuesta a un profundo misterio. Pero al intentar desentrañarlo las líneas comienzan a ondular frente a tus ojos. Se retuercen, giran, se arremolinan. Mareado, tienes que apartar la mirada.";
			} else {
				print "Las ondulantes líneas blancas parecen retorcerse en la superficie del mármol. No eres capaz de mirarlas fijamente sin marearte.";
			}
			new_line;
			return true;
		],
		before [;
			if (Baron.is_blind) {
				print "No puedes ver nada parecido entre las sombras.";
				new_line;
				return true;
			}
			Touch:
				<<Touch Marmol>>;
		],
;

!! @PuertasGruta
Item	-> PuertasGruta "puertas de bronce"
 has	door female openable ~open pluralname scenery transparent,
 with	name_f 'entrada' 'puerta' 'salida',
		name_fp 'puertas',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'bronce' 'cerrada' 'cerradas',
		description [;
			if (Baron.is_blind) {
				print "Las puertas se han cerrado a tu espalda dejándolo todo en penumbras.";
			} else if (self hasnt described) {
				print "Dos imponentes [self](puertas) de bronce bloquean la salida de la gruta. Están grabadas con toscas formas ornamentales entre las adviertes la [GrabadoHombreArdiendo](figura) de un hombre. Parece arder en llamas.";
			} else {
				print "Dos imponentes [self](puertas) de bronce bloquean la salida de la gruta. Están adornadas con el [GrabadoHombreArdiendo](grabado) de un hombre que arde en llamas.";
			}
			new_line;
			return true;
		],
		before [;
			Attack,
			KnockOn:
				if (Baron.is_burning) {
					print "Corres hacia las puertas presa de un pánico inefable. El hombre ardiendo en llamas grabado en el bronce te recibe con los ojos risueños y una sonrisa exultante antes tus gritos. —¡Disfruta del calor!— te dice sin palabras —Aprenderás a amarlo—. Lo golpeas con toda la fuerza de la que eres capaz ferozmente con el guantelete de acero de tu mano derecha; el sonido brutal, de una disonancia desgarradora, resuena contra las paredes de la cueva.";
					new_line;
					Utils.press_any_key();
					new_line;
					print "Pero las puertas no se mueven. Nadie acude a abrir. Chillando de dolor mientras el calor de las llamas te vaporiza la sangre de las venas golpeas de nuevo y el áspero sonido desgarra el vestíbulo una vez más, como si Satanás golpeara un diabólico gong en las profundidades del Infierno. Pero la puerta sigue permaneciendo inmóvil. Intentas golpear una vez más, pero tus rodillas se doblan. Tus piernas son incapaces de sostenerte por más tiempo y te desplomas en el suelo. La mente embotada por el dolor. Calor... No ves más que oscuridad...";
					new_line;
					EndSceneOne();
					return true;
				} else {
					print "¿Sin haberte enfrentado antes al dragón? No. No eres un cobarde.";
					new_line;
					return true;
				}
			Close:
				print "Ya están cerradas.";
				new_line;
				return true;
			Lock,
			Unlock:
				print "No parece haber ningún cerrojo.";
				new_line;
				return true;
			Open:
				print "Las [self](puertas) han sido trancadas desde el otro lado y ni siquiera el dragón podría abrirlas desde aquí. Afortunadamente. El acuerdo es que llamarás con un golpe para que te abran, una vez hayas derrotado al dragón.";
				new_line;
				return true;
		],
;

!! @GrabadoHombreArdiendo
Item	-> -> GrabadoHombreArdiendo "hombre en llamas"
 has	male,
 with	name_f 'figura' 'persona' 'talla',
		name_m 'caballero' 'grabado' 'hombre',
		gender G_MASCULINO,
		adjectives 'ardiendo' 'llamas' 'un' 'una',
		description [;
			print "En lugar de la expresión de tormento que cabría esperar de un hombre ardiendo, el rostro de la figura grabada en el bronce refleja algo muy distinto; ansia. Anhelo.";
			if (Baron.is_burning) {
				print " Horrorizado comprendes que tu propia cara debe estar mostrando exactamente lo mismo.";
			} else {
				print " Lejos de agonizar en las llamas la figura parece estar deleitándose en ellas.";
			}
			new_line;
			return true;
		],
		before [;
			if (Baron.is_blind) {
				print "No puedes ver nada parecido entre las sombras.";
				new_line;
				return true;
			}
			Touch:
				<<Touch PuertasGruta>>;
		],
;


!!==============================================================================
!!
!!	2)	Un pequeño pasadizo
!!
!!------------------------------------------------------------------------------

!! @DirGrutaPasadizo
AdjacentRoom	DirGrutaPasadizo "pasadizo"
 has	male remarkable,
 with	found_in GrutaVestibulo GrutaDragon,
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		adjacent_room GrutaPasadizo,
		before [;
			Enter:
				switch (real_location) {
					GrutaDragon:
						GrutaPasadizo.PC_is_coming_in = false;
					GrutaVestibulo:
						if (Baron.is_blind) {
							print "Es más sensato aguardar a que tus pupilas se terminen de acostumbrar a la [Oscuridad](oscuridad) antes de seguir adentrándote en las profundidades de la gruta.";
							new_line;
							return true;
						} else if (Baron.is_burning) {
							GrutaPasadizo.PC_is_coming_in = true;
							print "Corres desorientado, aplastando los huesos de aquellos que no tuvieron mejor suerte que tú frente al dragón.";
						} else {
							GrutaPasadizo.PC_is_coming_in = true;
							print "Haces lo posible por evitar la alfombra de restos humanos. Con cada paso, sin embargo, el chasquido de huesos aplastados bajo tus botas.";
						}
						new_line;
						new_line;
				}
				return self.AdjacentRoom::before();
		],
;

!! @GrutaPasadizo
Indoors	GrutaPasadizo "Un pequeño pasadizo"
 has	male,
 with	name_f 'cueva' 'gruta',
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		description [;
			if (Baron.is_burning) {
				print "A través del fuego y el dolor el espacio queda reducido a sólo un bosquejo.[if:compass_mode] [Al n_obj][else] [in_obj](Adentro)][fi]: el dragón; [if:compass_mode] [Al s_obj][else] [out_obj](afuera)[fi]: la salida.";
			} else {
				print "El techo del corredor es tan bajo que te obliga a inclinarte para poder pasar. Tus pisadas quedan amortiguadas por la gruesa capa de [Cenizas] que cubre el suelo. Una intensa [Fuego](luz) y oleadas de calor infernal te golpean de lleno procedentes[if:compass_mode] del final del pasadizo, [al n_obj][else] de la [DirGrutaDragon](guarida) del dragón, al final del pasadizo[fi]. Empapado de sudor dentro de la [Armadura](armadura), empiezas a echar de menos ya el frescor relativo del[if:compass_mode] del vestíbulo, [al s_obj][else] del [DirGrutaVestibulo](vestíbulo)[fi].";
			}
			new_line;
			return true;
		],
		before [;
			Listen:
				print "Puedes escuchar un movimiento desde[if:compass_mode] [el n_obj][else] la [DirGrutaDragon](guarida) del dragón[fi]; como un murmullo ronco.";
				new_line;
				return true;
			Smell:
				if (noun == 0) {
					print "El penetrante olor del azufre llena la cueva.";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		in_to [;
			<<Enter DirGrutaDragon>>;
		],
		out_to [;
			<<Enter DirGrutaVestibulo>>;
		],
		cant_go [;
			print "No puedes ir por ahí. ";
			return self.exits();
		],
		exits [;
			if (self.PC_is_coming_in) {
				print "Las únicas salidas visibles son el [DirGrutaVestibulo](vestíbulo), a tu espalda, y la [DirGrutaDragon](guarida) del dragón, frente a ti.";
			} else {
				print "Las únicas salidas visibles son el [DirGrutaVestibulo](vestíbulo), frente a ti, y la [DirGrutaDragon](guarida) del dragón, a tu espalda.";
			}
			new_line;
			return true;
		],
		PC_is_coming_in true,
;

Item	-> Bolso "bolso"
 with	name_f 'bandolera' 'bolsa',
		name_m 'bolso',
		gender G_MASCULINO,
		adjectives 'silvia' 'su',
		article "tu",
 has	male open;

Item	-> Llaves "llaves"
 with	name_f 'llave' 'muñeca' 'muñequita' 'sally',
		name_fp 'llaves',
		name_m 'llavero' 'muñeco' 'muñequito',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'silvia' 'su',
		article	"tus",
 has	female pluralname;

!! @Cenizas
Atrezzo	-> Cenizas "cenizas"
 has	female pluralname,
 with	name_f 'ceniza',
		name_fp 'cenizas',
		gender G_FEMENINO + G_PLURAL,
		description [;
			print "Una capa de cenizas cubre el suelo. Están tan compactadas que tus pies apenas dejan huella.";
			new_line;
			return true;
		],
		react_before [;
			if (noun == d_obj) {
				noun = self;
			}
		],
		before [;
			Take:
				print "Te inclinas para coger un puñado de cenizas pero están muy comprimidas. Sólo consigues manchar ligeramente el guantelete con el fino polvo.";
				new_line;
				return true;
		],
;

Atrezzo	-> "huellas"
 has	female pluralname,
 with	name_f 'huella',
		name_fp 'huellas',
		name_m 'rastro',
		gender G_FEMENINO + G_PLURAL,
		description [;
			print "Prácticamente invisibles en la compacta capa de cenizas.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	2)	Guarida del dragón
!!
!!------------------------------------------------------------------------------

!! @DirGrutaDragon
AdjacentRoom	DirGrutaDragon "guarida del dragón"
 has	male,
 with	found_in GrutaPasadizo,
		name_f 'cueva' 'estancia' 'gruta' 'guarida' 'sala',
		name_m 'salon',
		gender G_MASCULINO,
		adjectives 'dragon' 'enorme' 'gran' 'grande' 'subterraneo',
		adjacent_room GrutaDragon,
		description [;
			print "El dragón habita en el interior de la cueva. Se cuentan multitud de historias sobre cómo llegó hasta allí pero lo cierto es que nadie lo sabe realmente. Entre los sabios se pueden escuchar susurros asegurando que el dragón es tan viejo como la propia Tierra.";
			new_line;
			return true;
		],
		before [;
			Enter:
				if (Baron.is_burning) {
					print "Gritando y llorando de dolor, incapaz de pensar, corres de vuelta hasta el gran salón del dragón.";
				} else if (Baron.is_running_away) {
					print "El estruendo procedente de la gran gruta subterránea te hiela la sangre. No... no eres capaz de enfrentarte a ese monstruo. Sólo queda huir.";
					new_line;
					return true;
				} else if (self.PC_hesitates) {
					self.PC_hesitates = false;
					print "La entrada a la [self](guarida), al final del corredor, se muestra como la misma entrada del averno. Por un momento dudas si serás capaz de hallar en tu interior el valor necesario para dar el paso.";
					new_line;
					Tutorial.hesitations_hint();
					return true;
				} else {
					!! Comprueba el hacha:
					if (IndirectlyContains(player, HachaGuerra)) {
						print "Sujetando el hacha con firmeza";
					} else {
						print "Desarmado";
					}
					!! Comprueba el casco:
					if (IndirectlyContains(player, Casco) && Casco has worn) {
						if (CascoVisera has open) {
							print ", el visor del casco abierto —tal y como corresponde a un caballero honorable—";
						} else {
							print ", el visor del casco cerrado —menos honorable pero más prundente—";
						}
					} else {
						print ", valiente y gallardo sin tu casco";
					}
					print ", haces caso omiso al miedo y te dispones a plantar cara a la bestia.";
				}
				new_line;
				new_line;
				return self.AdjacentRoom::before();
		],
 private
		PC_hesitates true,
;

!! @GrutaDragon
Indoors	GrutaDragon "Enorme salón subterráneo"
 has	male,
 with	name_f 'cueva' 'estancia' 'gruta' 'guarida' 'sala',
		name_m 'salon',
		gender G_MASCULINO,
		adjectives 'dragon' 'enorme' 'gran' 'grande' 'subterraneo',
		initial [; if (self hasnt visited) {
			StartDaemon(Dragon);
			Baron.is_running_away = true;
		}],
		description [;
			if (Baron.is_burning) {
				print "La cueva a tu alrededor se desdibuja en el fulgor de las llamas. Entre la vorágine ardiente tan sólo distingues imágenes fragmentadas: [Fisuras] en la roca; la mancha carmesí [del Dragon], frente a ti; el pequeño punto de oscuridad que señala la boca del [DirGrutaPasadizo](pasadizo).";
			} else {
				print "El[if:compass_mode] pasadizo [s_obj][else] [DirGrutaPasadizo](pasadizo)[fi] desemboca en lo que parece una amplia gruta natural, mucho mayor incluso que la sala del trono del Emperador. Desde infinidad de [Fisuras](grietas) y fisuras abiertas en la roca emergen en chorro unas [Fuego](llamaradas) gigantescas. La luz aquí dentro es más potente que la del pleno día y el calor tan intenso que apenas se puede soportar.";
			}
			new_line;
			return true;
		],
		before [;
			Listen:
				if (noun == 0) {
					noun = Dragon;
				}
			Smell:
				if (noun == 0) {
					print "El penetrante olor del azufre llena la cueva.";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		cant_go [;
			if (Baron.is_burning) {
				!! TODO - repasar
				print "Corres desorientado, abrumado por el horror y la desesperación, incapaz de ver dónde colocas los pies. A tu alrededor sólo hay fuego, llamas, humo, dolor, luz cegadora. Tropiezas y caes al suelo pero aún eres capaz de volver a incorporarte impulsado por miedo y tratas de continuar a ciegas. Entonces un paso en la nada. Caes por una de las fisuras enormes abiertas en el suelo. Desesperadamente intentas aferrarte a algo, lo que sea, pero las paredes caen verticales sin ningún saliente que detenga tu caída hacia las profundidades de la tierra. El humo y los vapores de sulfuro llenan el aire y se meten agónicamente en tus pulmones con cada inspiración. Pierdes el conocimiento mucho antes de tocar el suelo.";
				new_line;
				EndSceneOne();
				return true;
			}
		],
;

!! @Dragon
Character	-> Dragon "dragón"
 has	animate concealed male,
 with	name_f 'bestia' 'monstruosidad',
		name_m 'dragon' 'engendro' 'monstruo',
		gender G_MASCULINO,
		adjectives 'carmesi' 'roja' 'rojo',
		description [;
			!! TODO - ¿si el Barón está ardiendo?
			!! TODO - ¿si se examina de nuevo?
			print "Aunque creías haberte preparado para lo peor, no estabas preparado para esto.
			^^
			Es grande, desde luego; calculas al menos una treintena de varas desde la punta de la cola hasta la testa. Pero ya te has enfrentado a monstruos de gran tamaño antes. Sus escamas rojas refulgen con el brillo del acero endurecido y pareciera que ningún arma corriente pudiese atravesarlas";
			if (IndirectlyContains(player, HachaGuerra)) {
				print ". Pero el tuyo no es ningún hacha ordinario";
			} else {
				print ". Pero ninguna criatura es invulnerable a un golpe certero";
			}
			print ". Y sin embargo... el *fuego*. Ése que se adivina en sus ojos y en su garganta, el baile de llamas tras la blanca hilera de dientes afilados. Ese fuego es el que imbuye tu alma con un horror como nunca lo ha hecho cosa alguna antes. No se trata de otro engendro escupe-fuego cualquiera, ni tiene nada que ver con un elemental —esas criaturas irracionales en quienes se encarnan las fuerzas primordiales de la naturaleza—. No, esto no es natural. Esto es mucho más aterrador, más insondable, más perverso. Sabes muy bien que ese fuego no podrá ser sometido en absoluto, ningún hombre podrá jamás doblegarlo y obligarlo a ceder a sus deseos, pues lo reconoces y cualquier otra persona lo reconocería también. Este monstruo, esta bestia, este fuego abrasador habita en el rincón más profundo del alma de todos nosotros. Siempre lo ha hecho y siempre lo hará. Y sabes que no hay modo de derrotarlo. Mas si estás aquí es precisamente para intentarlo.";
			new_line;
			return true;
		],
		before [
			previous_style;
			DefaultAction:
				if (self has described) {
					previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
					print (string) TEXT_STYLE_PARSER_PREFIX;
					print "ataca ", (al_) self;
					PrintToBuffer(temp_array, INPUT_BUFFER_LEN, self);
					PrintCharacterArray(temp_array);
					print (string) TEXT_STYLE_PARSER_SUFIX;
					Utils.set_text_style(previous_style);
					new_line;
					<<Attack self>>;
				}
		],
		life [;
			Attack:
				if (Baron.is_burning) {
					!! 2o. ataque. El dragón mata al Barón:
					print "Con la consciencia completamente aletargada por el dolor, tu cuerpo parece actuar solo, instintivamente. Y como fuera de ti le observas recomponerse y atacar de nuevo; el monstruo debe morir, sin importar el precio. Durante un instante alcanzas a ver los orbes maliciosos de sus ojos a través de las llamas";
					if (IndirectlyContains(player, HachaGuerra)) {
						print " y levantas el hacha dispuesto a descargarlo directamente entre ellos";
					} else {
						print " y preparas el puño dispuesto a intentar cegarlo";
					}
					print ". Entonces la cola del dragón se agita y te golpea en un costado, enviándote volando por la gruta en un vertiginoso arco que finaliza abruptamente con tu cabeza impactando contra la pared";
					if (IndirectlyContains(player, Casco) && Casco has worn) {
						print ". El casco amortigua en parte el golpe y evita que pierdas el conocimiento. Aún llegas a ver la cabeza grotesca acercándose a ti. Su boca abierta de par en par. Una nueva tempestad de fuego. La muerte llega rápido.";
					} else {
						print ". El cráneo se fractura en el acto y todo se vuelve negro.";
					}
					new_line;
					EndSceneOne();
					return true;
				} else {
					!! 1er. ataque. El dragón hace arder al Baron:
					self.burns_PC();
					if (IndirectlyContains(player, HachaGuerra)) {
						print "Alzas el hacha con ambas manos";
					} else {
						print "Levantas un puño furioso";
					}
					print ". —¡Escucha bien, bestia! —gritas, sorprendido de la seguridad de tus propias palabras. —¡Ha llegado tu hora! —y por un instante la estampa resulta heroica, la de un caballero de verdad sacado de algún cuento de hadas.
					^^
					El monstruo te observa impasible mientras cargas contra él, sus dientes como una vara de largos. Pero cargas a pesar de todo, espoleado por una rabia suicida. En ese momento, veloz y repentino como un relámpago, un surtidor de fuego estalla desde el interior de su boca y te envuelve por completo. El dolor supera cualquier límite de lo posible.";
					new_line;
					new_line;
					GrutaDragon.description();
					return true;
				}
			ThrownAt:
				!! TODO - Estudiar uso
				return false;
		],
		!! El daemon gestiona el movimiento del dragón, sus intentos por
		!! incinerar o comerse al Barón y la posible muerte del Barón por culpa
		!! de las llamas:
		daemon [;
			if (~~Baron.is_burning) {
				switch (self.counter) {
					0:
						new_line;
						print "La enorme mole de escamas rojas [del self] yace en el suelo en mitad de la cueva, observándote fijamente con un gran ojo amarillo.";
						new_line;
					1:
						if (real_location == GrutaDragon) {
							new_line;
							print "[El self] se incorpora sobre sus enormes patas provocando un gran temblor que sacude toda la cueva y que a punto está de tirarte al suelo. Sólo gracias a tus rápidos reflejos y a décadas de experiencia llevando armaduras pesadas eres capaz de mantener el equilibrio.";
							new_line;
						}
						if (real_location == GrutaPasadizo) {
							!! TODO - repasar
							new_line;
							print "La visión del monstruo es demasiado terrible incluso para un guerrero curtido como tú, así que huyes a la carrera. Y mientras lo haces la bestia se revuelve a tus espaldas sacudiéndo la cueva entera y amenazando con hacerte perder el equilibro. Un chillido antinatural resuena a lo largo de la gruta. No piensa dejarte escapar.";
							new_line;
						}
					2:
						if (real_location == GrutaDragon) {
							self.spit_fire_a();
						}
						if (real_location == GrutaPasadizo) {
							!! TODO - repasar
							new_line;
							print "El dragón encaja su enome cabeza en el interior del corredor. Un fuego diabólico se adivina en el interior de su garganta.";
							new_line;
						}
						if (real_location == GrutaVestibulo) {
							!! TODO - repasar
							new_line;
							print "Continuas huyendo del monstruo en una carrera desesperada, destrozando huesos a tu paso. Un humo oscuro empieza a surgir a tu espalda en cantidades enormes.";
							new_line;
						}
					3:
						if (real_location == GrutaDragon) {
							self.spit_fire_a();
						}
						if (real_location == GrutaPasadizo) {
							self.spit_fire_b();
						}
						if (real_location == GrutaVestibulo) {
							!! TODO - repasar
							new_line;
							print "El pasadizo que conduce hasta la cueva del dragón se llena de llamas, llamas que arden con el calor del sol, llamas que se propagan a toda velocidad en tu dirección como un tentáculo diabólico buscando a su presa.";
							new_line;
						}
					4:
						if (real_location == GrutaDragon) {
							self.spit_fire_a();
						}
						if (real_location == GrutaPasadizo) {
							self.spit_fire_b();
						}
						if (real_location == GrutaVestibulo) {
							self.spit_fire_c();
						}
				}
			} else if (self.counter > 3 && real_location == GrutaDragon) {
				!! TODO - repasar
				new_line;
				print "Envuelto en la vorágine de llamas eres incapaz de ver al dragón acercándose y cuando lo notas a tu lado ya es demasiado tarde; sus dientes atraviesan la armadura con la misma facilidad que si se tratase de la piel de una manzana. Conoces tu destino. Las mandíbulas de la bestia se cierran con un crujido repugnante —¿acaso la armadura? ¿tus propios huesos?—. Un torrente de sangre te inunda la boca. El alivio de la muerte no se hace esperar.";
				new_line;
				EndSceneOne();
				return true;
			}
			self.counter++;
			return true;
		],
 private
		burns_PC [;
			Baron.is_burning = true;
			StartDaemon(Baron);
			!! FIXME - pasar fuego al Barón
		],
		spit_fire_a [;
			!! TODO - repasar
			self.burns_PC();
			new_line;
			print "Un inmenso chorro de fuego sale escupido de la boca del dragón y te envuelve antes siquiera de que puedas pensar en apartarte. El dolor escapa a la comprensión.";
			new_line;
			return true;
		],
		spit_fire_b [;
			!! TODO - repasar
			self.burns_PC();
			new_line;
			print "Un inmenso chorro de fuego sale despedido de la boca abierta del dragón al fondo del corredor y te envuelve antes siquiera de que puedas pensar en echar a correr. El dolor está más allá de cualquier límite de la comprensión.";
			new_line;
			return true;
		],
		spit_fire_c [;
			!! TODO - repasar
			self.burns_PC();
			new_line;
			print "Un alarido sobrenatural atraviesa el aire. Al instante un torrente de llamas estalla desde la boca del pasillo y ocupa el vestíbulo. No puedes escapar. El repentino dolor cuando el fuego te alcanza está más allá de cualquier límite de la comprensión.";
			new_line;
			return true;
		],
		counter 0,
;

!! @Fisuras
Atrezzo	-> Fisuras "fisuras"
 has	container enterable female open ~openable pluralname,
 with	name_f 'fisura' 'grieta' 'hendedura' 'hendidura',
		name_fp 'fisuras' 'grietas' 'hendeduras' 'hendiduras',
		gender G_FEMENINO + G_PLURAL,
		description [;
			print "Amplias grietas perforan la roca por doquier y escupen llamas más altas que tú mismo.";
			new_line;
			return true;
		],
		before [;
			Enter:
				if (Baron.is_burning) {
					print "Decides poner fin al sufrimiento lo antes posible saltando al interior de una de las profundas fisuras que rompen el suelo. Y caes. A través del fuego y del humo caes. La grieta es mucho más profunda de lo que imaginabas y sigues cayendo hasta que de pronto la hendidura se abre en una gigantesca bóveda subterránea que alberga un lago de lava líquida en el fondo. Por una fracción del latido de tu corazón quedas fascinado ante la belleza arrebatadora que se muestra ante tus ojos pero entonces impactas contra la lava. Todo tu cuerpo es abrasado y arde y tu alma se transforma en fuego, en llamas, en calor, en luz.";
					EndSceneOne();
					return true;
				} else {
					print "Ahí sólo aguarda la muerte.";
				}
				new_line;
				return true;
		],
;
