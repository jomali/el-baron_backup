

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena primera
!!
!!==============================================================================
!!
!!	Archivo:		baron_scene-one.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2019/02/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2019, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	0)
!!
!!------------------------------------------------------------------------------

[ EndSceneOne;
	Baron.is_blind = false;
	Baron.is_burning = false;
	Baron.is_running_away = false;
	StopDaemon(Baron);
	StopDaemon(Dragon);
	StopDaemon(GrutaVestibulo);
	move Armadura to GrutaVestibulo;
	move Casco to GrutaVestibulo;
	move HachaGuerra to GrutaVestibulo;
];

!! @Tutorial
Object	Tutorial "(Tutorial)"
 has	scenery,
 with	hint_actions [;
			if (~~self.hint_actions_printed) {
				self.hint_actions_on = true;
				give self on;
			}
		],
		hint_hesitations [;
			if (~~self.hint_hesitations_printed) {
				self.hint_hesitations_on = true;
				give self on;
			}
		],
		hint_keywords_a [;
			if (~~self.hint_keywords_a_printed) {
				self.hint_keywords_a_on = true;
				give self on;
			}
		],
		hint_keywords_b [;
			if (~~self.hint_keywords_b_printed) {
				self.hint_keywords_b_on = true;
				give self on;
			}
		],
		hint_keywords_c [;
			if (~~self.hint_keywords_c_printed) {
				self.hint_keywords_c_on = true;
				give self on;
			}
		],
		!! Las pistas del tutorial se imprimen al mismo tiempo que el inductor
		!! (ver el objeto 'LibraryMessages' en 'baron_langLM.h'):
		hints [
			previous_style;
			if (self has on) {
				give self ~on;
				new_line;
				previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
				print (string) TEXT_STYLE_PARSER_PREFIX;
				if (self.hint_actions_on) self.hint_actions_text();
				if (self.hint_hesitations_on) self.hint_hesitations_text();
				if (self.hint_keywords_a_on) self.hint_keywords_a_text();
				if (self.hint_keywords_b_on) self.hint_keywords_b_text();
				if (self.hint_keywords_c_on) self.hint_keywords_c_text();
				print (string) TEXT_STYLE_PARSER_SUFIX;
				Utils.set_text_style(previous_style);
				new_line;
				return true;
			}
		],
 private
		are_hyperlinks_enabled [;
			if (Utils.get_hyperlinks_status(1)
				&& Utils.get_hyperlinks_status(2)
				&& Utils.get_hyperlinks_status(3)
				&& Utils.get_hyperlinks_status(4)) {
				return true;
			} else {
				return false;
			}
		],
		hint_actions_on false,
		hint_actions_printed false,
		hint_actions_text [;
			self.hint_actions_on = false;
			self.hint_actions_printed = true;
			print "Tutorial: Puedes realizar acciones escribiendo un verbo en imperativo junto a una palabra destacada asociada a un objeto.";
			if (location == GrutaVestibulo) {
				print "Puedes intentar, por ejemplo, ver las diferencias entre";
				if (self.are_hyperlinks_enabled()) print " hacer ~clic~ sobre";
				else print " teclear";
				print " PUERTAS, y teclear ABRE PUERTAS o LLAMA A LAS PUERTAS.";
			}
			if (location == GrutaPasadizo) {
				print "Puedes intentar, por ejemplo, ver las diferencias entre";
				if (self.are_hyperlinks_enabled()) print " hacer ~clic~ sobre";
				else print " teclear";
				print " CASCO, y teclear CIERRA CASCO o QUÍTATE EL CASCO.";
			}
			return true;
		],
		hint_hesitations_on false,
		hint_hesitations_printed false,
		hint_hesitations_text [;
			self.hint_hesitations_on = false;
			self.hint_hesitations_printed = true;
			print "Tutorial: Cuando el protagonista da muestras de duda o indecisión suele significar que estás a punto de iniciar una acción con consecuencias irreversibles. Si de verdad quieres seguir adelante, simplemente vuelve a";
			if (self.are_hyperlinks_enabled()) print " utilizar";
			else print " teclear";
			print " la misma acción de nuevo.";
			return true;
		],
		hint_keywords_a_on false,
		hint_keywords_a_printed false,
		hint_keywords_a_text [;
			self.hint_keywords_a_on = false;
			self.hint_keywords_a_printed = true;
			print "Tutorial:";
			if (self.are_hyperlinks_enabled()) print " Haz ~clic~ sobre";
			else print " Teclea";
			print " las palabras destacadas que vayas encontrando, como OSCURIDAD o RESPLANDOR, para hacer avanzar la historia. Tecla ";
			!! Utils.set_text_style(previous_style);
			PRT__("resaltado", 0, 2);
			Utils.set_text_style(TEXT_STYLE_PARSER);
			print " si estas palabras no aparecían con un estilo destacado en el párrafo anterior (o si prefieres modificar su aspecto).";
			return true;
		],
		hint_keywords_b_on false,
		hint_keywords_b_printed false,
		hint_keywords_b_text [;
			self.hint_keywords_b_on = false;
			self.hint_keywords_b_printed = true;
			print "Tutorial Aquellos ";
			!! Utils.set_text_style(previous_style);
			PRT__("objetos", 0, 2);
			Utils.set_text_style(TEXT_STYLE_PARSER);
			print " con los que puedas interactuar aparecerán destacados con este estilo de texto. En general, al";
			if (self.are_hyperlinks_enabled()) print " hacer ~clic~ sobre";
			else print " teclear";
			print " el nombre de cualquiera de estos objetos el protagonista lo examinará y podrás conseguir más información sobre ellos.";
			return true;
		],
		hint_keywords_c_on false,
		hint_keywords_c_printed false,
		hint_keywords_c_text [;
			self.hint_keywords_c_on = false;
			self.hint_keywords_c_printed = true;
			print "Tutorial: Hay un segundo tipo de palabra destacada relacionado con las ";
			!! Utils.set_text_style(previous_style);
			PRT__("salidas", 0, 3);
			Utils.set_text_style(TEXT_STYLE_PARSER);
			print ". Puedes";
			if (self.are_hyperlinks_enabled()) print " hacer ~clic~ sobre";
			else print " teclear";
			print " el nombre de una salida para desplazarte de una localidad a otra.";
			return true;
		],
;

!! @Armadura
Item	Armadura
 class	Clothes,
 has	male,
 with	short_name [;
			print "conjunto completo de [](armadura:2)";
			return true;
		],
		name_f 'armadura' 'cota' 'malla',
		name_fp 'cintas' 'mallas' 'placas',
		name_m 'guantelete',
		name_mp 'guanteles',
		gender G_MASCULINO,
		adjectives 'acero' 'completo' 'conjunto' 'cuero',
		!!------------------
;

!! @Casco
Item	Casco "casco"
 class	Clothes,
 has	male transparent,
 with	name_m 'casco' 'morrion' 'yelmo',
		gender G_MASCULINO,
		adjectives 'funcional',
		article "tu",
		!!------------------
		after [;
			Disrobe:
				move self to parent(player);
				print "Te quitas [el self] y lo posas en el suelo. Sin él no puedes evitar sentirte vulnerable.";
				new_line;
				return true;
			Take:
				give self worn;
				print "Lo recoges del suelo y te lo pones.";
				new_line;
				return true;
		],
		before [;
			Close:
				<<Close CascoVisera>>;
			Drop:
				if (self in player && self has worn) {
					<<Disrobe self>>;
				}
			Open:
				<<Open CascoVisera>>;
			Wear:
				if (TestScope(self, player) && self notin player) {
					<<Take self>>;
				}
		],
		description [;
			print "Un [self](casco:0) funcional, sin adornos de ningún tipo.";
			if (self in player && self has worn) {
				if (CascoVisera has open) {
					print " [La CascoVisera] está abierta, dejando libre todo el campo visual pero exponiendo tu rostro ante un eventual ataque.";
				} else {
					print " [La CascoVisera] está cerrada, ofreciendo la máxima protección.";
				}
			} else {
				if (CascoVisera has open) {
					print " Tiene [la CascoVisera] abierta.";
				} else {
					print " Tiene [la CascoVisera] cerrada.";
				}
			}
			new_line;
			return true;
		],
;

!! @CascoVisera
!! TODO - Aceptar los adjetivos abierto/cerrado contextualmente (modificando
!! la rutina de parsing del propio objeto)
Item	CascoVisera "visera" Casco
 has	female open openable,
 with	name_f 'visera',
		name_m 'visor',
		gender G_FEMENINO,
		adjectives 'casco',
		!!------------------
		after [;
			Close:
				print "Cierras la [self](visera:0) [del Casco].";
				new_line;
				return true;
			Open:
				print "Abres la [self](visera:0) [del Casco].";
				new_line;
				return true;
		],
		before [;
			DefaultAction:
				if (self has open) {
					<<Close self>>;
				} else {
					<<Open self>>;
				}
			Examine:
				<<Examine Casco>>;
		],
;

!! @GrutaFuego
Atrezzo	GrutaFuego "fuego"
 has	male,
 with	found_in GrutaDragon GrutaPasadizo GrutaVestibulo,
		name_f 'claridad' 'llama' 'luz',
		name_fp 'llamas',
		name_m 'fuego' 'fulgor' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		!!------------------
;

!! @HachaGuerra
Item	HachaGuerra
 has	male,
 with	short_name [;
			print "[](hacha:2) de guerra";
			return true;
		],
		name_m 'arma' 'hacha',
		gender G_MASCULINO,
		adjectives 'batalla' 'guerra',
		article "tu",
		!!------------------
;


!!==============================================================================
!!
!!	1)	Vestíbulo de la gruta
!!
!!------------------------------------------------------------------------------

!! @DirGrutaVestibulo
AdjacentRoom	DirGrutaVestibulo "vestíbulo"
 has	male,
 with	found_in GrutaPasadizo,
		name_f 'antesala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!------------------
		adjacent_room GrutaVestibulo,
;

!! @GrutaVestibulo
Indoors	GrutaVestibulo "Vestíbulo de la gruta"
 has	male,
 with	name_f 'antesala' 'caverna' 'cueva' 'estancia' 'gruta' 'sala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!------------------
		before [;
			Listen:
				if (noun == 0 or self) {
					if (Baron.is_blind) {
						print "Desde las profundidades de la gruta";
					} else {
						print "Desde[if:compass_mode] [el n_obj][else] [el DirGrutaPasadizo][fi]";
					}
					print " llega una especie de gruñido ronco.";
					new_line;
					return true;
				} else if (TestScope(noun, player) && ~~noun.before()) {
					print "Prestas atención [al noun] pero no escuchas nada extraño. El único sonido que llega hasta tus oídos es esa especie de gruñido grave que parece surgir de las profundidades de la cueva.";
					new_line;
					return true;
				}
			Smell:
				if (noun == 0 or self) {
					print "El penetrante olor del azufre llena la [self](cueva).";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		daemon [ flag;
			!! Los ojos del Barón se acostumbran automáticamente a la oscuridad
			!! de la gruta tras "limit_a + limit_b" turnos:
			if (self.counter < (self.limit_a + self.limit_b)) self.counter++;
			if (self.counter == (self.limit_a + self.limit_b)) {
				Baron.is_blind = false;
				StopDaemon(self);
			}
			return true;
		],
		description [;
			if (Baron.is_burning) {
				print "A través de las llamas alcanzas a distinguir [Marmol](mármol:2) negro. [Huesos](Huesos:2) en el suelo. Las dos [GrutaPuertas](puertas:2) de bronce que bloquean la única salida.";
			} else {
				Tutorial.hint_keywords_a();
				print "Esta pequeña [self](sala) rectangular ha sido excavada en la roca y sus paredes y su techo están ahora recubiertos por planchas de [Marmol](mármol:2) negro. Dos [GrutaPuertas] sellan la única salida. Frente a ellas, en la pared opuesta del vestíbulo, un estrecho [DirGrutaPasadizo](corredor) conduce a la ardiente guarida del dragón. Hay toda una alfombra de [Huesos] y [Huesos](cráneos:2) ennegrecidos. [Oscuridad](Sombras) grotescas en la luz titilante.";
			}
			new_line;
			return true;
		],
		exits [;
			print "Aparte de la salida bloqueada por [las GrutaPuertas] cerradas, únicamente se puede ir hacia [in_obj](adentro). Hacia el cubil del dragón.";
			new_line;
			return true;
		],
		initial [;
			if (self hasnt visited) {
				move Armadura to Baron;
				move Casco to Baron;
				move HachaGuerra to Baron;
				give Armadura worn;
				give Casco worn;
				Baron.is_blind = true;
 				StartDaemon(self);
				print "^^^^
				Las [GrutaPuertas](puertas:1) de bronce se cierran a tu espalda y escuchas el chirrido de pesados cerrojos deslizándose en su interior. Está bien; te puedes encargar de esto tú solo. Con el [HachaGuerra](hacha:1) en alto das un paso adelante, hacia el calor.
				^^^^";
				Banner();
				new_line;
				return true;
			}
		],
		in_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		n_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaPasadizo>>;
			else return self.cant_go();
		],
 private
		counter 0,
		limit_a 3, ! > 0
		limit_b 4, ! > 0
;

!! @Baron
Character	-> Baron
 has	male proper,
 with	name_m 'personaje' 'protagonista',
		gender G_MASCULINO,
		!!------------------
		is_blind false,
		is_burning false,
		is_running_away false,
;

!! @GrutaPuertas
Item	-> GrutaPuertas
 has	door female openable ~open pluralname scenery transparent,
 with	short_name [;
			print "[](puertas:2) de bronce";
			return true;
		],
		name_f 'entrada' 'puerta' 'salida',
		name_fp 'puertas',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'bronce' 'cerrada' 'cerradas',
	!!------------------
;

!! @GrutaPuertasGrabado
Atrezzo	-> -> GrutaPuertasGrabado
 has	male,
 with	short_name [;
			print "[](hombre) en llamas";
			return true;
		],
		name_f 'figura' 'persona' 'talla',
		name_m 'aforno' 'caballero' 'grabado' 'hombre',
		gender G_MASCULINO,
		adjectives 'ardiendo' 'llamas' 'un' 'una',
	!!------------------
;

!! @Huesos
Atrezzo	-> Huesos "huesos"
 has	male pluralname,
 with	name_f 'calavera',
		name_fp 'calaveras',
		name_m 'craneo' 'esqueleto' 'hueso',
		name_mp 'craneos' 'esqueleto' 'huesos',
		gender G_MASCULINO + G_PLURAL,
		adjectives 'depedazado' 'despedazados' 'ennegrecida' 'ennegrecidas'
			'ennegrecido' 'ennegrecidos' 'humana' 'humanas' 'humano' 'humanos'
			'negra' 'negras' 'negro' 'negros',
		!!------------------
;

!! @Marmol
Atrezzo	-> Marmol "mármol"
 has	female pluralname transparent,
 with	name_f 'pared',
		name_fp 'paredes',
		name_m 'marmol',
		gender G_MASCULINO,
		adjectives 'negra' 'negras' 'negro' 'planchas' 'pulida' 'pulidas'
			'pulido',
		!!------------------
		description [;
			print "Tanto las paredes como el techo están cubiertos por unas placas de [self] negro muy pulido. Sus vetas forman [MarmolVetas](patrones:2) intrincados sobre la superficie.";
			new_line;
			return true;
		],
;

!! @MarmolVetas
Atrezzo	-> MarmolVetas "vetas"
 has	male pluralname,
 with	name_f 'estria' 'linea' 'raya' 'veta',
		name_fp 'estrias' 'lineas' 'rayas' 'vetas',
		name_m 'patron',
		name_mp 'patrones',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'blanca' 'blancas' 'blanco' 'blancos' 'marmol',
		!!------------------
;

!! @Oscuridad
Atrezzo	-> Oscuridad "oscuridad"
 has	female,
 with	name_f 'negrura' 'oscuridad' 'sombra' 'tiniebla',
		name_fp 'sombras' 'tinieblas',
		gender G_FEMENINO,
		adjectives 'temblorosa' 'temblorosas' 'titilante' 'titilantes',
		!!------------------
;


!!==============================================================================
!!
!!	2)	Un pequeño pasadizo
!!
!!------------------------------------------------------------------------------

!! @DirGrutaPasadizo
AdjacentRoom	DirGrutaPasadizo "pasadizo"
 has	male,
 with	found_in GrutaDragon GrutaVestibulo,
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		!!------------------
		adjacent_room GrutaPasadizo,
		before [;
			Enter:
				switch (location) {
					GrutaDragon:
						GrutaPasadizo.PC_is_coming_in = false;
					GrutaVestibulo:
						if (Baron.is_blind) {
							print "Es más sensato aguardar a que tus pupilas se terminen de acostumbrarse a la [Oscuridad] antes de seguir adentrándote en la cueva.";
							new_line;
							return true;
						} else if (Baron.is_burning) {
							GrutaPasadizo.PC_is_coming_in = true;
							print "Corres desorientado, aplastando los huesos de aquellos que no tuvieron mejor suerte que tú al plantar cara al dragón.";
						} else {
							GrutaPasadizo.PC_is_coming_in = true;
							print "Haces lo posible por evitar los restos humanos. A cada paso, sin embargo, el chasquido de huesos aplastados bajo tus botas.";
						}
						new_line;
						new_line;
				}
				return self.AdjacentRoom::before();
		],
;

!! @GrutaPasadizo
Indoors	GrutaPasadizo "Un pequeño pasadizo"
 has	male,
 with	name_f 'caverna' 'cueva' 'gruta',
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		!!------------------
		before [;
			Listen:
				if (noun == 0 or self) {
					print "Puedes escuchar una especie de murmullo ronco que llega desde[if:compass_mode] [el n_obj][else] [el DirGrutaDragon][fi].";
					new_line;
					return true;
				} else if (TestScope(noun, player) && ~~noun.before()) {
					print "Prestas atención [al noun] pero no escuchas nada extraño. Nada aparte del crepitar del fuego y el murmullo ronco que llegan desde[if:compass_mode] [el n_obj][else] [el DirGrutaDragon][fi].";
					new_line;
					return true;
				}
			Smell:
				if (noun == 0 or self) {
					print "El penetrante olor del azufre llena la [self](cueva).";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		description [;
			if (Baron.is_burning) {
				print "El mundo a tu alrededor es sólo un bosquejo agónico bajo [el GrutaFuego].[if:compass_mode] [Al n_obj][else] [in_obj](Adentro)[fi]: el dragón. [if:compass_mode] [Al s_obj][else] [out_obj](Afuera)[fi]: la salida.";
			} else {
				print "El techo del [self](corredor) es tan bajo que tienes que inclinarte para poder pasar. Una capa de [Cenizas](cenizas:2) amortigua tus pisadas. Desde [la DirGrutaDragon], en el extremo[if:compass_mode] [n_obj][fi] del [self](pasillo), la potente luz [del GrutaFuego] y oleadas de un calor infernal";
				if (self.PC_is_coming_in) {
					print " te golpean en plena cara";
				} else {
					print " te abrasan la espalda";
				}
				print " y provocan que estés empapado en sudor dentro de tu [Armadura](armadura). [El DirGrutaVestibulo][if:compass_mode] [al s_obj][fi] se antoja ahora un remanso de frescor.";
			}
			new_line;
			return true;
		],
		exits [;
			if (self.PC_is_coming_in) {
				print "Tienes [el DirGrutaVestibulo] a tu espalda y [el DirGrutaDragon] frente a ti.";
			} else {
				print "Tienes [el DirGrutaVestibulo] frente a ti y [el DirGrutaDragon] a tu espalda.";
			}
			new_line;
			return true;
		],
		in_to [;
			<<Enter DirGrutaDragon>>;
		],
		n_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaDragon>>;
			else return self.cant_go();
		],
		out_to [;
			<<Enter DirGrutaVestibulo>>;
		],
		!! Registra si el Barón está entrando o saliendo de la cueva:
		PC_is_coming_in true,
		s_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaVestibulo>>;
			else return self.cant_go();
		],
;

!! @Cenizas
Atrezzo	-> Cenizas "cenizas"
 has	female pluralname transparent,
 with	name_f 'ceniza',
		name_fp 'cenizas',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'capa' 'de' 'suelo',
		!!------------------
		react_before [;
			if (noun == d_obj) {
				noun = self;
			}
		],
;

Atrezzo	-> -> "huellas"
 has	female pluralname,
 with	name_f 'huella',
		name_fp 'huellas',
		name_m 'rastro',
		gender G_FEMENINO + G_PLURAL,
		!!------------------
		description [;
			print "Prácticamente invisibles en la compacta capa de [Cenizas].";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	2)	Guarida del dragón
!!
!!------------------------------------------------------------------------------

!! @DirGrutaDragon
AdjacentRoom	DirGrutaDragon
 has	female,
 with	found_in GrutaPasadizo,
		short_name [;
			print "[](guarida:3) del dragón";
			return true;
		],
		name_f 'guarida' 'madriguera',
		name_m 'cubil' 'nido',
		gender G_FEMENINO,
		adjectives 'bestia' 'dragon' 'monstruo',
		!!------------------
		adjacent_room GrutaDragon,
		before [;
			Enter:
				if (Baron.is_running_away) {
					!! TODO
				} else if (Baron.is_burning) {
					!! TODO
				} else if (self.PC_hesitates) {
					self.PC_hesitates = false;
					Tutorial.hint_hesitations();
					print "La entrada al cubil parece la entrada misma a los avernos. Existen multitud de leyendas acerca de cómo el monstruo llegó a vivir ahí, pero lo cierto es que nadie lo sabe con certeza. Algunos sabios afirman que el dragón es tan antiguo como la propia Tierra y que ha habitado en las profundidades de la gruta desde el origen de los tiempos.
					^^
					Por un momento dudas, preguntándote si podrás hallar en tu interior el valor necesario para dar el paso.";
					new_line;
					return true;
				} else {
					!! Comprueba el hacha:
					if (IndirectlyContains(player, HachaGuerra)) {
						print "Aferrando el [HachaGuerra](hacha:1) con firmeza";
					} else {
						print "Desarmado";
					}
					!! Comprueba el casco:
					if (IndirectlyContains(player, Casco) && Casco has worn) {
						if (CascoVisera has open) {
							print ", el visor del [Casco](casco:1) abierto —tal y como dictan los códigos—";
						} else {
							print ", el visor del [Casco](casco:1) cerrado —no tan honorable, pero más prudente—";
						}
					} else {
						print ", arrogante y bravo sin tu casco";
					}
					print ", apartas los temores a un lado y te dispones a encarar a la bestia.";
				}
				new_line;
				new_line;
				return self.AdjacentRoom::before();
		],
 private
		PC_hesitates true,
;

!! @GrutaDragon
Indoors	GrutaDragon "Enorme salón subterráneo"
 has	male,
 with	name_f 'caverna' 'cueva' 'estancia' 'gruta' 'guarida' 'madriguera'
			'sala',
		name_m 'cubil' 'nido' 'salon',
		gender G_MASCULINO,
		adjectives 'dragon' 'enorme' 'gran' 'grande' 'subterraneo',
		!!------------------
		before [;
			Listen:
				if (noun == 0 or self) {
					!! <<Listen Dragon>> !! TODO
				} else if (TestScope(noun, player) && ~~noun.before()) {
					!! TODO PRT__
					print "El estruendo de las llamas y del propio dragón apagan cualquier otro sonido.";
					new_line;
					return true;
				}
			Smell:
				if (noun == 0 or self) {
					print "El penetrante olor del azufre llena la [self](cueva).";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		cant_go [;
			if (Baron.is_burning) {
				!! TODO
				return true;
			}
			return self.Room::cant_go();
		],
		exits [;
			print "La boca [del DirGrutaPasadizo] se abre en una de las paredes de roca del enorme [self](salón).";
			new_line;
			return true;
		],
		initial [;
			if (self hasnt visited) {
				Baron.is_running_away = true;
				StartDaemon(Dragon);
			}
		],
		description [;
			if (Baron.is_burning) {
				print "Entre la vorágine de [GrutaFuego](llamas:1) sólo aciertas a distinguir imágenes fragmentadas de la [self](cueva); [Grietas](fisuras:2) en la roca, la mancha carmesí del [Dragon], el pequeño punto de oscuridad que señala la boca [del DirGrutaPasadizo].";
			} else {
				print "[if:compass_mode]El pasadizo [s_obj][else][El DirGrutaPasadizo][fi] desemboca en lo que parece una amplia [self](gruta) natural, mucho mayor incluso que la sala del trono del Emperador. Desde infinidad de [Grietas](grietas:2) y [Grietas](fisuras) abiertas en la roca emergen [GrutaFuego](llamaradas:2) gigantescas. La luz aquí dentro es más brillante que el mediodía y el calor resulta insoportable.";
			}
			new_line;
			return true;
		],
		out_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		s_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaPasadizo>>;
			else return self.cant_go();
		],
;

!! @Dragon
Character	-> Dragon "dragón"
 has	concealed male,
 with	name_f 'bestia' 'monstruosidad',
		name_m 'dragon' 'engendro' 'monstruo',
		gender G_MASCULINO,
		adjectives 'carmesi' 'roja' 'rojo',
		article "el",
		!!------------------
;

!! @Grietas
Atrezzo	-> Grietas "grietas"
 has	container enterable female open ~openable pluralname,
 with	name_f 'fisura' 'grieta' 'hendedura' 'hendidura',
		name_fp 'fisuras' 'grietas' 'hendeduras' 'hendiduras',
		gender G_FEMENINO + G_PLURAL,
		!!------------------
;



Object bijl "battle axe"
  with description
          "For this fight you have brought your heaviest battle axe. Few beside you could even lift it, let alone handle it with lethal speed and precision.",
       name 'axe' 'battle' 'battle-axe' 'weapon',
   has ;


Object harnas "full suit of armour"
  with description
          [;
	     if(Baron.is_burning == 0) "You wouldn't want to face the monster without a full suit of armour, thick plate over chainmail, generously oiled, granting optimal protection against all kinds of physical attack. The suit has been fastened with leather straps at the back.";
	     "Even the steel of the armour is melting in the heat of the dragon's fires. White hot drops of liquid metal force their way through your skin like murderous knives.";
	   ],
       name 'armour' 'suit' 'of' 'full' 'straps' 'leather' 'chainmail',
       before [;
          Disrobe: "You can't take off the suit of armour yourself; it is secured with leather straps fastened around your back. After you have defeated the dragon, you will return through the bronze doors and your squire will help you."; ],
   has worn clothing;



[ Grot_Hal_N;
  if(Baron.is_burning == 0) {
       print "You try to step around the human bones, but with each step several are crushed under the combined weight of you and your armour.^";
       return false;
    } else {
  "You run around aimlessly, crushing the bones of those who fared no better against the dragon than you did.";
  }
];

   Object grot_deuren "bronze doors" GrutaVestibulo
    class Atrezzo,
     with name 'doors' 'bronze' 'two' 'door',
          description "Two massive bronze doors block the cave's exit. The likeness of a burning man has been engraved on them.",
	  before
	    [;
	      Open: "The doors have been bolted from the other side, and not even the dragon could open them from here. In fact, that is the entire point. You have been instructed to knock after you have defeated the dragon.";
	      Close: "The doors are already closed.";
	      Lock, Unlock: "There is no keyhole apparent.";
	      Knock: if(Baron.is_burning == 0) {
	                "Before you have even tried to kill the dragon? You are no coward.";
		     } else {
		        print "You run towards the doors in blind panic. The burning man in the bronze relief looks at you with laughing eyes, his ecstatic grin mocking your tortured cries. ~Enjoy the heat!~, he seems to be saying. ~You will learn to love it!~ You hit him as hard as you can with the steel glove on your right hand; a tremendous sound, harrowingly dissonant, resounds through the cave.";

			Utils.press_any_key();

			print "But the doors do not move. Nobody comes to open them. Again, screaming--the heat of the flames vaporising the blood in your veins--you hit the door with all the force you can muster; and again the harsh sound rings out, like Satan beating a gong in the depths of Hell--but again the door remains immobile. You try to knock once more, but your knees buckle, your legs fold beneath you and you hit the ground... you cannot think through the pain, the heat... you can only see darkness...";
			EndSceneOne(); return true;
		     }
	      Attack: <<Knock self>>;
	    ],
      has ~open openable pluralname;


      Object grot_man "burning man" GrutaVestibulo
       class Atrezzo,
        with name 'burning' 'man' 'picture' 'engraving' 'depiction',
	     description
	       [;
	          if(Baron.is_burning > 0)
		     "Instead of the pain you would expect on the face of a burning man, the graven figure's expression speaks of craving and desire. To your horror, you realise that your own face bears a similar expression.";

	          "Instead of the pain you would expect on the face of a burning man, the graven figure's expression speaks of craving and desire. He does not suffer from the flames, but basks in them.";

	       ],
	 has male;

      Object GrutaVestibulo_vuur "fire" GrutaVestibulo
       class Atrezzo,
        with name 'fire' 'flames' 'light',
	     description "You see vast, leaping flames beyond the passageway. Their heat can be felt even here.",
	     before [;
	       Doof:
	         if(Baron.is_burning == 0) {
				"You cannot extinguish such a sea of fire.";
			}
			else "In dismay, you beat on the flames. But to no avail--they only appear to grow bigger and hotter.";
	     ],
	 has ;

      Object grot_marmer "marble" GrutaVestibulo
       class Atrezzo,
        with name 'marble' 'black' 'sheets' 'walls' 'wall' 'ceiling' 'rock' 'slab' 'slabs',
	     description "The walls and the ceiling of the hall are covered with smooth sheets of black marble. White veins running through them form intricate patterns.",
	 has ;

         Object grot_aders "pattern" GrutaVestibulo
	  class Atrezzo,
	   with name 'vein' 'veins' 'pattern' 'patterns' 'lines' 'white',
	        description "As you let your gaze dwell over the patterns, you are struck by the thought that they somehow encode a meaning, that beneath their apparent chaos lies the solution to a grave mystery. But before you can even start understanding them, the lines begin to move, to spin, to swirl--you feel dizziness overtaking you, and quickly focus your eyes elsewhere.",
            has pluralname;

   Object grot_botten "bones" GrutaVestibulo
    class Atrezzo,
     with name 'bones' 'bone' 'skulls' 'skull' 'remains' 'skeleton' 'skeletons' 'ground' 'floor',
          description "The ground is covered by bones and skulls, blackened but otherwise untouched by the fire that consumed the flesh they once bore.",
	  before [;
	     Take: "You don't like to be reminded of the failure of those who came before you.";
	  ],
      has pluralname;

   Object grot_schaduwen "shadows" GrutaVestibulo
    class Atrezzo,
     with name 'shadows' 'shadow' 'flickering',
          description "The shadows of the bones take on macabre forms.",
      has pluralname;



[ Grot_Grot_S;
  if(Baron.is_burning == 0) {
       print "Bones crunch under your feet as you walk back into the vestibule.^";
       return false;
    } else {
  "You frantically run back to the vestibule, bones breaking under each of your steps.";
  }
];

[ Grot_Grot_N;
  if(Baron.is_burning == 0) {
       if(parent(bijl) == player) {print "Holding the axe tightly, ";} else {print "Unarmed, ";};
       if(parent(Casco) == player && Casco hasnt open) print "the visor of your helmet closed--less honourable, but more prudent--, ";
       if(parent(Casco) == player && Casco has open) print "the visor of your helmet opened, as behooves a man of honour, ";
       if(parent(Casco) ~= player) print "brave and gallant, without your helmet, ";
       print "you approach the dragon.^";
       return false;
    } else {
  "Screaming and crying, no room in your mind for thought, you run back to the dragon's hall.";
  }
];


   Object grot_as "thick layer of ashes" GrutaPasadizo
    class Atrezzo,
     with description
              "A layer of tamped down ashes covers the ground; your feet leave hardly an impression.",
	  name 'ash' 'layer' 'thick' 'ground' 'floor' 'ashes',
	  before [;
	     Take: "You bend down to the ground and scrape at the ashes with your armoured fingers, but they are compacted so tightly that almost nothing comes loose.";
	  ],
      has ;

    Object grot_fake_draak "dragon" GrutaPasadizo
      with name 'dragon',
           description
             "The dragon is in the large hall to the north.",
	   before [;
	     Examine: return false;
	     default: "You will first have to enter the hall to the north.";
	   ],
	   life [;
	     default: "You will first have to enter the hall to the north.";
	   ],
       has male animate scenery;

      Object grot_voetstap "footprints" GrutaPasadizo
       class Atrezzo,
        with description
	         "Your footprints are only just visible in the ashes.",
             name 'footprint' 'footprints' 'impression' 'impressions' 'footsteps' 'footstep',
         has pluralname;

      Object GrutaPasadizo_vuur "fire" GrutaPasadizo
       class Atrezzo,
        with name 'fire' 'flames' 'light' 'flame',
	     description "Flames bigger than men illuminate the hall to the north.",
	     before [;
	       Doof:
	         if(Baron.is_burning == 0) {"You cannot extinguish such a sea of fire.";}
                 else "In dismay, you beat on the flames. But to no avail--they only appear to grow bigger and hotter.";
	     ],
	 has ;



   Object grot_draak "dragon" GrutaDragon
     with description
             "You thought you expected the worst, but you were not prepared for this. Yes, it is large--at least thirty meters from the tip of its tail to its evil head--but you have fought large monsters before. Yes, its angry red scales gleam like hard steel and look as if no normal weapon could pierce them--but your axe is no normal weapon. But the fire in its eyes and its throat, the play of flames behind the dragon's rows of pale teeth, seems suddenly more horrifying than anything you ever saw before. This is not just another monster with fiery breath. This is not even an elemental, those mindless creatures that embody the very forces of nature. No--this is more terrifying, more intimate, more evil--something indomitable, something that will never yield and that can never be exploited, that unlike nature's laws will not yield to the desires of men--for you recognise this fire, this monster, this beast, and all men would recognise it, for it dwells within the innermost soul of all of us, and always has, and always will, and you know there is no way you can defeat it. And yet you must try.",
	  name 'dragon' 'enormous' 'scaly' 'infernal' 'monster' 'red' 'angry' 'beast',
	  counting 0,
	  daemon    ! This daemon regulates the movement of the dragon, his attempts to set the player on fire
	    [;      ! or eat him, and finally the player's possible death through burning.

	      if(self.counting == 0) {print "^The dragon, an enormous red scaly monster, lies on the ground in the middle of the hall. It stares at you with a big yellow eye.^";};

	      if(self.counting == 1 && Baron.is_burning == 0) {
	           if(parent(player) == GrutaDragon) {
		       print "^The dragon raises itself from the ground and approaches you, each heavy step rocking the cave floor and threatening to throw you off balance. But quick reflexes and decades of experience with heavy armour allow you to remain upright; and when you raise an angry fist at the monster, defying his threatening glare, you look positively heroic--a true knight, from a true faerytale.^";
		     };
		   if(parent(player) == GrutaPasadizo) {
		       print "^The sight of the monster is too much even for a hardened warrior such as yourself. As you run away, the thundering footsteps of the dragon, hot in pursuit, shake the floor of the cave and threaten to send you sprawling. Its blood-curdling cries echo through the subterranean vaults--it does not intend to let you escape.^";
		     };
	        };

	      if(self.counting == 2 && Baron.is_burning == 0) {
	           if(parent(player) == GrutaDragon) {
		       print "^An immense jet of fire spews forth from the dragon's mouth and envelops you before you can so much as think of stepping aside. The sudden pain is beyond comprehension.^";
		       Baron.is_burning = 1; move draak_vuur to player ;
		     };
		   if(parent(player) == GrutaPasadizo) {
		       print "^The dragon sticks its huge head into the passageway. Behind rows of vicious teeth burns an incinerating fire.^";
		     };
		   if(parent(player) == GrutaVestibulo) {
		       print "^Again, you run away from the monstrosity in wild panic, carelessly crushing human bones beneath your armoured feet. Smoke billows out of the cave in copious quantities.^";
		     };
	        }

	      if(self.counting == 3 && Baron.is_burning == 0) {
	           if(parent(player) == GrutaDragon) {
		       print "^An immense jet of fire spews forth from the dragon's mouth and envelops you before you can so much as think of stepping aside. The sudden pain is beyond comprehension.^";
		       Baron.is_burning = 1; move draak_vuur to player ;
		     };
		   if(parent(player) == GrutaPasadizo) {
		       print "^An immense jet of fire spews forth from the dragon's mouth at the north end of the passageway, and envelops you before you can so much as think of running away. The sudden pain is beyond comprehension.^";
		       Baron.is_burning = 1; move draak_vuur to player ;
		     };
		   if(parent(player) == GrutaVestibulo) {
		       print "^The passageway that leads to the dragon's cave fills up with flames, flames that burn as hot as the sun, flames that reach for you like a tentacled reaches for its prey.^";
		     };
	        };

	      if(self.counting == 4 && Baron.is_burning == 0) {
		   if(parent(player) == GrutaPasadizo) {
		       print "^An immense jet of fire spews forth from the dragon's mouth at the north end of the passageway, and envelops you before you can so much as think of running away. The sudden pain is beyond comprehension.^";
		       Baron.is_burning = 1; move draak_vuur to player ;
		     };
		   if(parent(player) == GrutaVestibulo) {
		       print "^The cry of the dragon, high and unearthly, pierces the air. Instantly, a torrent of flames burst from the passageway and into the vestibule. You cannot escape. The sudden pain as the fire engulfs you is beyond comprehension.^";
		       Baron.is_burning = 1; move draak_vuur to player ;
		     };
	        };

	      if(self.counting > 3 && parent(player) == GrutaDragon) {
	           print "^Enveloped in murderous flames, you do not see the dragon approaching; but when its teeth penetrate your armour (resisting no more than the soft skin of an apple), you become aware of your fate. With a sickening crushing sound--your armour? your bones?--the dragon brings its jaws together. A stream of blood flows out of your mouth. Death comes quickly and as a relief.^";
	           EndSceneOne();
	        };

	      if(Baron.is_burning == 2) {
	           print "^Flames surround you on all sides, and the metal of your armour is growing unbearably hot.^";
                };

	      if(Baron.is_burning == 3) {
	           print "^Every moment, your pain increases. You beat at the flames wildly, hoping to extinguish them--but to no avail. Worse, you realise with dread that your armour is slowly starting to melt.^";
                };

	      if(Baron.is_burning == 4) {
	           print "^Melting metal penetrates your skin and commingles with your liquifying flesh. No visions, no sounds, no thoughts: only pain fills your consciousness as you fall to your knees, screaming your last scream while your vocal cords burn away. Darkness falls over you like a cool blanket.^";
		   EndSceneOne();
                };

	      self.counting++;
              if(Baron.is_burning > 0) Baron.is_burning++;
	   ],
	   attack 0,
	   life [;
	     Attack, ThrowAt:

	       if(self.attack > 0 || Baron.is_burning > 0) {
	         print "With heroic willpower you resist the numbing pain and charge again: the monster must die, though your life be the price. Through the flames, you see the pale malicious circles of its eyes ";
		 if(parent(bijl) == player) print "and raise your axe to plant it right between them.";
		 if(parent(bijl) ~= player) print "and raise your fists in order to plant them right in them, blinding the monster.";
		 print " But suddenly the dragon's tail swings in from the side and hits you, sending you flying through the hall in dizzying arc that ends when your head crashes into a wall.";
		 if(Casco has worn) print " Your helmet softens the blow, and you quickly regain your senses. Unfortunately, when you open your eyes, the dragon's monstrous head is already approaching you, its mouth wide open--and once again it bathes you in deadly fire. Death comes quickly.^";
		 if(Casco hasnt worn) print " Your skull fractures immediately, and everything goes black.^";
		 EndSceneOne();
		 return true;
	        };

	       if(self.attack == 0 && Baron.is_burning == 0) {
	          if(parent(bijl) == player) print "Raising the axe, you step towards the dragon. ~Your knell has rung,~ you inform it, surprised at your own confidence. ~It's time to die.~^^";

	          if(parent(bijl) ~= player) print "Raising you armoured fists, you step towards the dragon. ~Your knell has rung,~ you inform it, surprised at your own confidence. ~It's time to die.~^^";

		  print "The monster looks at you impassively. You cannot help but stare at its double rows of arm-long teeth as you charge, but charge you do, screaming with justified rage. Then--as fast and sudden as lightning--a jet of fire bursts from dragon's the mouth and envelops you from head to toe. The pain is beyond comprehension.^";
		  Baron.is_burning = 1;
	        };

	       self.attack++;
	       return true;
	   ],
      has animate scenery neuter;


   Object grot_spleten "cracks" GrutaDragon
    class Atrezzo,
     with description
              "Broad cracks run through the rock everywhere, spitting out flames bigger than men.",
          name 'crack' 'cracks' 'fissure' 'fissures' 'cleft' 'clefts' 'crevice' 'crevices' 'broad',
          before [;
	     Enter:
	        if(Baron.is_burning > 0) {
		   print "You choose to end the pain as quickly as you can, and jump into one of the deep crevices that split the floor. Through fire and smoke you fall and fall, far longer than you had expected, until suddenly the crevice gives way to a subterranean vault spanning a lake of boiling lava. For a fraction of a heartbeat you stare in wonder at this beautiful sight--but then you fall into the lava, and your entire body melts and burns as your spirit and your soul become fire, flames, heat, light...";
		   EndSceneOne(); return true;
		 }
		else {
		  "All that awaits you there is death.";
		};
	   ],
      has pluralname container open enterable;

   Object GrutaDragon_vuur "fire" GrutaDragon
       class Atrezzo,
        with name 'fire' 'flames' 'light' 'flame' 'huge',
	     description "Huge flames jump from the ground wherever you look.",
	     before [;
	       Doof:
	         if(Baron.is_burning == 0) {"There is no way you could extinguish such a sea of fire.";}
                 else "In dismay, you beat on the flames. But to no avail--they only appear to grow bigger and hotter.";
	     ],
	 has ;


Object draak_vuur "devouring fire"
  with name 'flames' 'fire' 'devouring',
       description "A turbulent sea of flames envelops you.",
       before [;
         Drop: "If only it were that easy!";
	 Doof: "In dismay, you beat on the flames. But to no avail--they only appear to grow bigger and hotter.";
       ],
   has ;
