

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena primera
!!
!!==============================================================================
!!
!!	Archivo:		baronSceneOne.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2021/05/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2021, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	0)
!!
!!------------------------------------------------------------------------------

[ EndSceneOne
	obj previous_style;
	Utils.erase_window();

	!! Actualizaciones de estado:
	StopDaemon(Baron);
	StopDaemon(Dragon);
	StopDaemon(GrutaVestibulo);
	Baron.is_burning = false;
	Baron.is_running_away = false;
	move Armadura to Limbo;
	move Casco to Limbo;
	move Percunos to Limbo;

	if (Checking == 0) {
		!! Créditos:
		new_line;
		VersionSub();
		Utils.press_any_key();
		Utils.erase_window();

		!! Recapitulación de decisiones del usuario:
		new_line;
		print (important) "A continuación se listan las distintas decisiones y eventos que has experimentado a lo largo de la partida:";
		new_line;
		objectloop(obj in E1_Recapitulacion && obj has on) {
			new_line;
			previous_style = Utils.set_text_style(TEXT_STYLE_REVERSED);
			print "\[ ";
			PrintToBuffer(utility_array_a, INPUT_BUFFER_LEN, obj);
			PrintCharacterArray(utility_array_a, 1); ! uppercase
			print " \]";
			Utils.set_text_style(previous_style);
			print " − ", (string) obj.description;
			new_line;
			Utils.press_any_key();
		}
		new_line;
		print (important) "Las siguientes, en cambio, son distintas posibilidades que se han quedado sin explorar:";
		new_line;
		objectloop(obj in E1_Recapitulacion && obj hasnt on) {
			new_line;
			previous_style = Utils.set_text_style(TEXT_STYLE_NOTE);
			print "\[ ";
			PrintToBuffer(utility_array_a, INPUT_BUFFER_LEN, obj);
			PrintCharacterArray(utility_array_a, 1); ! uppercase
			print " \]";
			Utils.set_text_style(TEXT_STYLE_PARSER);
			print " − ", (string) obj.description;
			Utils.set_text_style(previous_style);
			new_line;
			Utils.press_any_key();
		}
		new_line;
		print (important) "Aquí termina la demostración.";
		new_line;
		new_line;
		print (parser) "Pulsa una tecla para salir.";
		Utils.press_any_key();
		quit;
	} else {
		new_line;
		print (strong) Story, "^";
		new_line;
		print "Una historia interactiva de Victor Gijsbers.^";
		print "Versión en castellano por J. Francisco Martín.^";
		new_line;
		Utils.press_any_key();
		Utils.erase_window();
		new_line;
		print (stressed) "Chi l'anima mi lacera?^
		Chi m'agita le viscere?^
		Che strazio, ohimè, che smania!^
		Che inferno, che terror!^";
		new_line;
		print (stressed) "«¿Quién es aquel que lacera mi alma?^
		¿Quién aquel que tortura mi cuerpo?^
		¡Qué tormento! ¡Oh! ¡Qué agonía!^
		¡Qué infierno! ¡Qué terror!»^";
		new_line;
		print "De la ópera ~Don Giovanni~^
		Texto: Lorenzo da Ponte^
		Música: Wolfgang A. Mozart^";
		new_line;
		Utils.press_any_key();
		Utils.erase_window();
		new_line;
		return PlayerTo(DormitorioBaron);
	}
];

!! @Armadura
Item	Armadura Limbo
 class	Clothes,
 has	male,
 with	short_name [;
			print "conjunto completo de [](armadura:REMARKABLE_OBJECT)";
			return true;
		],
		name_f 'armadura' 'cota' 'malla',
		name_fp 'cintas' 'mallas' 'placas',
		name_m 'guantelete',
		name_mp 'guanteles',
		gender G_MASCULINO,
		adjectives 'acero' 'completo' 'conjunto' 'cuero',
		!!------------------
		before [;
			Disrobe:
				if (Baron.is_burning()) {
					print "El metal fundido de la [self](armadura:OTHER_OBJECT) te perfora el cuerpo. Querrías quitártela. Arrancarte la piel. Pero no tienes cómo.";
				} else {
					print "No puedes quitarte la [self](armadura:OTHER_OBJECT) solo; está asegurada con apretadas cintas de cuero a la espalda.";
					if (~~self.tried_to_disrobe) {
						self.tried_to_disrobe = true;
						print " Una vez hayas derrotado al dragón y salgas de aquí podrás quitártela de encima con ayuda de tu escudero.";
					}
					new_line;
					return true;
				}
		],
		description [;
			if (Baron.is_burning()) {
				print "Incluso el acero de la [self](armadura:OTHER_OBJECT) se está fundiendo con el calor del fuego. Sientes las gotas de metal líquido abriéndose paso a través de tu piel como cuchillos.";
			} else {
				if (real_location == GrutaDragon or GrutaPasadizo) {
					print "Por su culpa estás sudando copiosamente, pero jamás";
				} else {
					print "Jamás";
				}
				print " te enfrentarías al dragón sin un conjunto completo de [self](armadura:OTHER_OBJECT). Gruesas placas de acero sobre cota de mallas, todo bien engrasado, que garantiza la mayor protección posible contra cualquier tipo de ataque físico. El conjunto está ajustado por unas cintas de cuero, a la espalda.";
			}
			new_line;
			return true;
		],
 private
		tried_to_disrobe false,
;

!! @Casco
Item	Casco "casco" Limbo
 class	Clothes,
 has	male transparent,
 with	name_m 'casco' 'morrion' 'yelmo',
		gender G_MASCULINO,
		adjectives 'funcional',
		article "tu",
		!!------------------
		after [;
			Disrobe:
				move self to parent(player);
				print "Te quitas [el self] y lo posas en el suelo. Sin él no puedes evitar sentirte vulnerable.";
				new_line;
				return true;
			Take:
				give self worn;
				print "Lo recoges del suelo y te lo pones.";
				new_line;
				return true;
		],
		before [;
			Close:
				<<Close CascoVisera>>;
			Drop:
				if (self in player && self has worn) {
					<<Disrobe self>>;
				}
			Open:
				<<Open CascoVisera>>;
			Wear:
				if (TestScope(self, player) && self notin player) {
					<<Take self>>;
				}
		],
		description [;
			print "Un [self](casco) funcional, sin adornos de ningún tipo.";
			if (self in player && self has worn) {
				if (CascoVisera has open) {
					print " [La CascoVisera] está abierta, dejando libre todo el campo visual pero exponiendo tu rostro ante un eventual ataque.";
				} else {
					print " [La CascoVisera] está cerrada, ofreciendo la máxima protección.";
				}
			} else {
				if (CascoVisera has open) {
					print " Tiene [la CascoVisera] abierta.";
				} else {
					print " Tiene [la CascoVisera] cerrada.";
				}
			}
			new_line;
			return true;
		],
;

!! @CascoVisera
!! TODO - Aceptar los adjetivos abierto/cerrado contextualmente (modificando
!! la rutina de parsing del propio objeto)
Item	-> -> CascoVisera "visera"
 has	female open openable,
 with	name_f 'visera',
		name_m 'visor',
		gender G_FEMENINO,
		adjectives 'casco',
		!!------------------
		after [;
			Close:
				print "Cierras la [self](visera) [del Casco].";
				new_line;
				return true;
			Open:
				print "Abres la [self](visera) [del Casco].";
				new_line;
				return true;
		],
		before [;
			DefaultAction:
				if (self has open) {
					<<Close self>>;
				} else {
					<<Open self>>;
				}
			Examine:
				<<Examine Casco>>;
		],
;

!! @Fuego
Item	Fuego "fuego" Limbo
 has	light male,
 with	name_f 'claridad' 'llama' 'luz',
		name_fp 'llamas',
		name_m 'fuego' 'fulgor' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		!!------------------
		!! TODO
;

!! FIXME
!! Object draak_vuur "devouring fire"
!!   with name 'flames' 'fire' 'devouring',
!!        description "A turbulent sea of flames envelops you.",
!!        before [;
!!          Drop: "If only it were that easy!";
!! 	 Doof: "In dismay, you beat on the flames. But to no avail--they only appear to grow bigger and hotter.";
!!        ],
!!    has ;

!! @Percunos
Item	Percunos Limbo
 has	male remarkable,
 with	short_name [;
			print "[](hacha:REMARKABLE_OBJECT) de guerra";
			return true;
		],
		name_m 'arma' 'hacha' 'percunos' 'perkwunos',
		gender G_MASCULINO,
		adjectives 'batalla' 'guerra',
		article "tu",
		!!------------------
		description [;
			print "Para esta gesta has traído el más pesado de tus hachas de guerra; *Percunos*. Pocos pueden levantar su peso, y menos aún manejarlo con tu misma velocidad y precisión mortíferas.";
			new_line;
			return true;
		],
;

!! @E1_Recapitulacion
Object	E1_Recapitulacion "(Escena Uno Recap.)" Limbo;

!! @E1_CodigoCaballeria
Object	-> -> E1_CodigoCaballeria "Código de caballería"
 with	description
			"Te has enfrentado al dragón respetando el código de honor de la orden de caballería; equipado con arma, casco y armadura, pero con el rostro descubierto para que la bestia pueda mirarte a los ojos en el momento de acabar con su vida.",
;

!! @E1_MasValePrevenir
Object	-> -> E1_MasValePrevenir "Más vale prevenir"
 with	description
			"Te has enfrentado al dragón asumiendo de antemano todas las precacuciones posibles; equipado con arma, casco y armadura, y con el visor del casco cerrado para protegerte el rostro.",
;

!! @E1_Berserker
Object	-> -> E1_Berserker "Berserker"
 with	description
			"Te has enfrentado al dragón sin tu casco y completamente desarmado.",
;

!! @E1_GuerreroObstinado
Object	-> -> E1_GuerreroObstinado "Guerrero obstinado"
 with	description
			"Has cargado contra el dragón incluso a pesar de estar envuelto en llamas, después de que éste lanzase su aliento de fuego sobre ti.",
;

!! @E1_ElHombreArdiente
Object	-> -> E1_ElHombreArdiente "El hombre ardiente"
 with	description
			"Has ardido durante varios turnos después de que el dragón haya escupido un chorro de fuego sobre ti, hasta llegar a ver cómo tu armadura empezaba a fundirse por el calor.",
;

!! @E1_UnGongInfernal
Object	-> -> E1_UnGongInfernal "Un gong infernal"
 with	description
			"Has aporreado las puertas de bronce intentando huir del fuego del dragón pero nadie ha respondido a tu llamada.",
;

!! @E1_YEseRuido
Object	-> -> E1_YEseRuido "¿Y ese ruido?"
 with	description
			"Lo último que has escuchado ha sido un desagradable crujido mientras el dragón se te comía vivo.",
;

!! @E1_ExcavandoDemasiadoProfundo
Object	-> -> E1_ExcavandoDemasiadoProfundo "Excavando demasiado profundo"
 with	description
			"Has muerto al caer en un lago de magma en lo más profundo de la gruta.",
;


!!==============================================================================
!!
!!	1)	Entre las sombras
!!
!!------------------------------------------------------------------------------

!! @GrutaSombras
Indoors	GrutaSombras
 has	concealed male,
 with	short_name [;
			print "Entre las sombras";
			return true;
		],
		gender G_MASCULINO,
		!!------------------
		description [;
			Tutorial.hint_highlight_key_a();
			if (GrutaSombrasOscuridad has described) {
				print "Distingues un [GrutaSombrasResplandor](fulgor) anaranjado y muy débil entre las sombras.";
			} else {
				print "Todo es [GrutaSombrasOscuridad] a tu alrededor. El mundo entero reducido a un vacío negro e impenetrable.";
			}
			new_line;
			return true;
		],
		exits [;
			if (GrutaSombrasOscuridad has described) {
				print "No sabrías a dónde dirigirte en [la GrutaSombrasOscuridad].";
			} else {
				print "Todo cuanto puedes distinguir en [la GrutaSombrasOscuridad] es ese [GrutaSombrasResplandor] indefinido que llega desde algún lugar más [f_obj].";
			}
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirGrutaSombrasAdelante>>;
		],
		initial [;
			if (self hasnt visited) {
				move Armadura to Baron;
				move Casco to Baron;
				move Percunos to Baron;
				give Armadura worn;
				give Casco worn;
				return true;
			}
		],
		n_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaSombrasAdelante>>;
			else return self.cant_go();
		],
;

!! @DirGrutaSombrasAdelante
AdjacentRoom	-> DirGrutaSombrasAdelante
 has	male,
 with	!!------------------
		adjacent_room GrutaVestibulo,
		before [ previous_style;
			Enter:
				self.AdjacentRoom::onChangeLocation();
				if (GrutaSombrasOscuridad hasnt described) {
					print "Puedes distinguir un resplandor entre las sombras. ";
				}
				print "Tan pronto das un paso en la dirección de la que proviene la luz, las dos grandes [GrutaPuertas](puertas) de bronce se cierran a tu espalda con un golpe atronador seguido por el chirrido de los cerrojos deslizándose en su interior. Ahora ya no hay marcha atrás, ha llegado el momento de enfrentarte a la bestia. Aferras el [Percunos](hacha) con firmeza y das otro paso adelante, hacia el calor…";
				new_line;

!				previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
!				print (string) TEXT_STYLE_PARSER_PREFIX;
!				VorplePlaceElement("a", nothing, "Continuar…");
!				print (string) TEXT_STYLE_PARSER_SUFIX;
!				Utils.set_text_style(previous_style);

				self.AdjacentRoom::before();
				return true;
		],
		description [;
			<<Examine GrutaSombrasResplandor>>;
		],
;

!! @GrutaSombrasOscuridad
Background	-> GrutaSombrasOscuridad "oscuridad"
 has	female on,
 with	name_f 'oscuridad' 'sombra' 'tiniebla',
		name_fp 'sombras' 'tinieblas',
		name_m 'vacio',
		gender G_FEMENINO,
		adjectives 'negro',
		!!------------------
		description [;
			if (self has described) {
				<<Look>>;
			} else {
				Tutorial.hint_highlight_key_b();
				print "Las [self](sombras) te abrazan cómplices, como si formases parte de este lugar. Solo e inmóvil en mitad de la nada, casi desearías poder quedarte aquí para siempre; limitarte a cerrar los ojos, desentenderte de todo, descansar por fin. Pero no es más que una idea infantil, por supuesto.
				^^
				Desde algún lugar llega [un GrutaSombrasResplandor] impreciso.";
			}
			new_line;
			return true;
		],
		react_before [;
			if (self hasnt on) return false;
			Consult,
			Examine,
			Look,
			LookUnder,
			Read,
			Search:
				if (noun == nothing) return false;
				if (noun == self) return false;
				if (noun == parent(self)) return false;
				if (noun == GrutaSombrasResplandor) return false;
				return self.cant_see();
			DefaultAction:
				return false;
			Drop:
				if (noun == nothing) return false;
				if (~~IndirectlyContains(player, noun)) return false;
				print "Si dejas [el noun] aquí es posible que luego no puedas encontrarl[o noun] de nuevo en [la self].";
				new_line;
				return true;
			Inv,
			InvTall,
			InvWide:
				return self.cant_see();
			Rub:
				if (noun == nothing) return false;
				if (noun == self) return false;
				if (noun == parent(self)) return false;
				if (noun ofclass Background) return false;
				print "Es difícil limpiar [el noun] —o cualquier otra cosa— en completa oscuridad.";
				new_line;
				return true;
		],
 private
		cant_see [;
			if (self has described) {
				print "No puedes ver nada en [la self], aparte de [un GrutaSombrasResplandor] muy débil que llega desde algún lugar más [f_obj].";
			} else {
				print "No puedes ver nada en [la self].";
			}
			new_line;
			return true;
		],
;

!! @GrutaSombrasResplandor
Background	-> GrutaSombrasResplandor "resplandor"
 has	male,
 with	name_f 'luz',
		name_m 'fulgor' 'resplandor',
		gender G_MASCULINO,
		adjectives 'anaranjado' 'impreciso' 'tenue',
		!!------------------
		description [;
			print "Se trata de una luz parpadeante, muy tenue. Llega desde algún lugar más [f_obj].";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	2)	Vestíbulo de la gruta
!!
!!------------------------------------------------------------------------------

!! @DirGrutaVestibulo
AdjacentRoom	DirGrutaVestibulo "vestíbulo"
 has	male,
 with	found_in GrutaPasadizo,
		name_f 'antesala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!------------------
		adjacent_room GrutaVestibulo,
		before [;
			Enter:
				if (Baron.is_running_away()) {
					if (Baron.is_burning()) {
						print "Corres desesperadamente de vuelta al [GrutaVestibulo](vestíbulo), destrozando a tu paso [los Huesos] del suelo.";
					} else {
						print "Continúas alejándote del monstruo a la carrera. El crujido de [los Huesos] aplastados a tu paso.";
					}
				} else {
					print "Regresas al [GrutaVestibulo](vestíbulo), destrozando a tu paso [los Huesos] que alfombran el suelo.";
				}
				new_line;
				self.AdjacentRoom::before();
				return true;
		],
		description [;
			print "El frescor relativo [del self] aguarda un poco más allá.";
			new_line;
			return true;
		],
;

!! TODO - actualizar PC_is_coming_in
!! @GrutaVestibulo
Indoors	GrutaVestibulo
 has	male,
 with	short_name [;
			print "[](Vestíbulo) de la gruta";
			return true;
		],
		name_f 'antesala' 'caverna' 'cueva' 'estancia' 'gruta' 'sala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!------------------
		b_to [;
			<<Enter GrutaPuertas>>;
		],
		before [;
			Listen:
				if (Baron.is_burning) {
					print "El fuego ruge en tus oídos como un animal hambriento.";
				} else if (noun == 0 or self) {
					print "Una especie de reverberación grave y discontinua parece propagarse por las paredes.";
				} else if (TestScope(noun, player) && ~~noun.before()) {
					print "Prestas atención [al noun] pero no escuchas nada extraño. El único sonido que llega hasta tus oídos es esa especie de gruñido grave que parece surgir de las profundidades de la gruta.";
				}
				new_line;
				return true;
			Smell:
				if (noun == 0 or self) {
					print "Un penetrante olor como a huevos podridos llena la [self](cueva).";
				} else {
					print "Un hedor como a huevos podridos lo impregna todo.";
				}
				new_line;
				return true;
		],
		description [;
			if (Baron.is_burning()) {
				print "A través de las llamas alcanzas a distinguir [Marmol](mármol:REMARKABLE_OBJECT) negro. [Huesos](Huesos:REMARKABLE_OBJECT) en el suelo. Las dos [GrutaPuertas](puertas:REMARKABLE_OBJECT) de bronce que bloquean la única salida.";
			} else {
				Tutorial.hint_keywords_a();
				print "Esta pequeña sala rectangular ha sido excavada en la roca y sus paredes y su techo han sido recubiertas después con planchas de [Marmol](mármol) negro. Dos [GrutaPuertas] sellan la única salida. Frente a ellas, en la pared opuesta del vestíbulo, un estrecho [DirGrutaPasadizo](corredor) se interna aún más en las ardientes profundidades de la montaña.";
			}
			new_line;
			return true;
		],
		exits [;
			if (Baron.is_burning()) {
				print "Las [GrutaPuertas](puertas:DIRECTION_OBJECT) son la única salida de este infierno.";
			} else {
				print "Aparte de la salida bloqueada por [las GrutaPuertas] cerradas, únicamente se puede seguir [f_obj]. Hacia el cubil del monstruo.";
			}
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		in_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		n_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaPasadizo>>;
			else return self.cant_go();
		],
 private
		counter 0,
;

!! @GrutaPuertas
Item	-> GrutaPuertas
 has	door female openable ~open pluralname scenery transparent,
 with	short_name [;
			print "[](puertas:REMARKABLE_OBJECT) de bronce";
			return true;
		],
		name_f 'entrada' 'puerta' 'salida',
		name_fp 'puertas',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'bronce' 'cerrada' 'cerradas',
		!!------------------
		before [;
			!! TODO Touch/Pull/Push
			Attack,
			KnockOn:
				if (Dragon.counter > 0) {
					if (Baron.is_burning()) {
						give E1_UnGongInfernal on;
						print "Te abalanzas contra las puertas de bronce enloquecido por el dolor";
						if (self has described) {
							print ". El grabado del hombre ardiente te recibe con los ojos risueños y una sonrisa exultante. —¡Disfruta del calor!— te dice sin palabras —Aprenderás a amarlo—. Lo golpeas ferozmente con toda la fuerza de la que eres capaz y tu";
						} else {
							print " y las golpeas ferozmente con toda la fuerza de la que eres capaz. Tu";
						}
						print " guantelete arranca un sonido brutal al bronce. Una atroz disonancia que resuena contra las paredes de la cueva.";
						new_line;
						Utils.press_any_key();
						new_line;
						print "Sin embargo las puertas no se mueven. Nadie acude a abrir. Chillando de dolor mientras el calor de las llamas vaporiza la sangre de tus venas golpeas de nuevo y el áspero sonido desgarra el vestíbulo una vez más, como si el mismo Satanás estuviese golpeando un gong diabólico en las profundidades del Infierno. Pero la puerta permanece inmóvil. Nadie responde a tu llamada. Estás echando el puño hacia atrás para golpear otra vez cuando tus rodillas se doblan. Tus piernas no pueden sostenerte por más tiempo y te desplomas en el suelo. La mente embotada por el dolor. Calor… Oscuridad… No ves nada más.";
						new_line;
						print "^^^", (parser) "Pulsa una tecla.";
						Utils.press_any_key();
						return EndSceneOne();
					} else if (self.state == 0) {
						self.state = 1;
						print "Golpeas frenéticamente sobre su superficie de bronce pero no responde nadie.";
					} else {
						print "Insistes entre sollozos, desesperado. *Abridme. Abrid… Por favor…*";
					}
				} else {
					print "¿Sin haberte enfrentado antes al dragón? No. No eres un cobarde.";
				}
				new_line;
				return true;
			Lock,
			Unlock:
				print "No parece haber ningún cerrojo. No en este lado.";
				new_line;
				return true;
			Open:
				print "Las [self](puertas) han sido trancadas desde el otro lado y ni siquiera el dragón podría abrirlas desde aquí. Esa es la idea. El acuerdo es que llamarás con un golpe para que te abran una vez hayas derrotado al monstruo.";
				new_line;
				return true;
		],
		description [;
			!! TODO Barón en llamas
			print "Dos imponentes [self] bloquean la única salida de la gruta.";
			if (self hasnt described) {
				print " Tienen unos grabados ornamentales, de una factura muy tosca, entre los que adviertes la figura de un [GrutaPuertasGrabado](hombre:REMARKABLE_OBJECT) que parece arder en llamas.";
			} else {
				print " Están decoradas con el [GrutaPuertasGrabado](grabado) de un hombre que arde en llamas.";
			}
			new_line;
			return true;
		],
;

!! @GrutaPuertasGrabado
Atrezzo	-> -> GrutaPuertasGrabado
 has	male,
 with	short_name [;
			print "[](hombre) en llamas";
			return true;
		],
		name_f 'figura' 'persona' 'talla',
		name_m 'adorno' 'caballero' 'grabado' 'hombre',
		gender G_MASCULINO,
		adjectives 'ardiendo' 'llamas' 'un' 'una',
		!!------------------
		before [
			obj;
			Pull,
			Push,
			PushDir,
			Turn:
				obj = parent(self);
				<<Push obj>>;
			Take:
				obj = parent(self);
				<<Take obj>>;
			Touch:
				obj = parent(self);
				<<Touch obj>>;
		],
		description [;
			print "En lugar de la expresión de tormento que cabría esperar de un hombre ardiendo, el rostro de la figura grabada en el bronce refleja algo muy distinto: ansia, anhelo.";
			if (Baron.is_burning()) {
				print " Horrorizado comprendes que tu propia cara debe estar mostrando exactamente lo mismo en este momento.";
			} else {
				print " Lejos de agonizar en las llamas la figura parece estar deleitándose en ellas.";
			}
			new_line;
			return true;
		],
;

!! @GrutaVestibuloFuego
Atrezzo	-> GrutaVestibuloFuego "fuego"
 class	Background,
 has	light male,
 with	name_f 'claridad' 'llama' 'luz',
		name_fp 'llamas',
		name_m 'fuego' 'fulgor' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		!!------------------
		before [;
			Blow,
			SwitchOff:
				print "Nadie podría apagar un fuego como ese.";
				new_line;
				return true;
		],
		description [;
			print "Un incendio salvaje arde en las profundidades de la gruta y arroja destellos de una luz rojiza y anaranjada a tu alrededor.";
			new_line;
			return true;
		],
;

!! @Huesos
Atrezzo	-> Huesos "huesos"
 has	male pluralname,
 with	name_f 'calavera',
		name_fp 'calaveras',
		name_m 'craneo' 'esqueleto' 'hueso',
		name_mp 'craneos' 'esqueleto' 'huesos',
		gender G_MASCULINO + G_PLURAL,
		adjectives 'depedazado' 'despedazados' 'ennegrecida' 'ennegrecidas'
			'ennegrecido' 'ennegrecidos' 'humana' 'humanas' 'humano' 'humanos'
			'negra' 'negras' 'negro' 'negros',
		!!------------------
		before [;
			Take:
				print "Son los restos de aquellos que vinieron aquí antes que tú, y no tienes ninguna intención de cargar con recuerdos de su fracaso.";
				new_line;
				return true;
		],
		describe [;
			new_line;
			print "Hay toda una alfombra de [self](huesos) y [self](cráneos) ennegrecidos. Arrojan sombras grotescas en la luz titilante.";
			new_line;
			return true;
		],
		description [;
			print "El suelo está cubierto de esqueletos despedazados. Negros por el fuego que en algún momento consumió la carne de sus cuerpos.";
			new_line;
			return true;
		],
;

!! @Marmol
Atrezzo	-> Marmol "mármol"
 has	female pluralname transparent,
 with	name_f 'pared',
		name_fp 'paredes',
		name_m 'marmol',
		gender G_MASCULINO,
		adjectives 'negra' 'negras' 'negro' 'planchas' 'pulida' 'pulidas'
			'pulido',
		!!------------------
		before [;
			!! TODO Touch/Pull/Push
		],
		description [;
			print "Tanto las paredes como el techo están cubiertos por unas placas de [self] negro muy pulido. Sus vetas forman [MarmolVetas](patrones:2) intrincados sobre la superficie.";
			new_line;
			return true;
		],
;

!! @MarmolVetas
Atrezzo	-> -> MarmolVetas "vetas"
 has	male pluralname,
 with	name_f 'estria' 'linea' 'raya' 'veta',
		name_fp 'estrias' 'lineas' 'rayas' 'vetas',
		name_m 'patron',
		name_mp 'patrones',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'blanca' 'blancas' 'blanco' 'blancos' 'marmol',
		!!------------------
		before [
			obj;
			Pull,
			Push,
			PushDir,
			Turn:
				obj = parent(self);
				<<Push obj>>;
			Take:
				obj = parent(self);
				<<Take obj>>;
			Touch:
				obj = parent(self);
				<<Touch obj>>;
		],
		description [;
			if (self hasnt described) {
				print "Al estudiar las líneas del mármol se apodera de ti el presentimiento de que, de algún modo, esas estrías blancas codifican un significado oculto. Como si bajo el aparente caos se hayase la respuesta a un profundo misterio. Pero al esforzarte por desentrañarlo los patrones comienzan a ondular ante tus ojos; y se retuercen y giran y se arremolinan unos con otros. Mareado, tienes que apartar la mirada.";
			} else {
				print "Las ondulante líneas blancas parecen retorcerse en la superficie [del Marmol]. No eres capaz de mirarlas fijamente sin marearte.";
			}
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	2)	Un pequeño pasadizo
!!
!!------------------------------------------------------------------------------

!! @DirGrutaPasadizo
AdjacentRoom	DirGrutaPasadizo "pasadizo"
 has	male,
 with	found_in GrutaDragon GrutaVestibulo,
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		!!------------------
		adjacent_room GrutaPasadizo,
		before [;
			Enter:
				switch (location) {
					GrutaDragon:
						GrutaPasadizo.PC_is_coming_in = false;
						if (Baron.is_running_away()) {
							if (Baron.is_burning()) {
								print "Corres envuelto en llamas en busca de la salida.";
							} else {
								Dragon.counter = 1;
								if (Dragon has described) {
									print "Ese fuego es demasiado terrible";
								} else {
									print "Hay algo… algo en la mirada del monstruo que resulta demasiado terrible";
								}
								print " aun para un guerrero curtido como tú. Presa del miedo, huyes.";
							}
							new_line;
						}
					GrutaVestibulo:
						if (Dragon.counter == 4) {
							print "Al girarte la visión es tan horrible que te deja clavado en el sitio.";
							new_line;
							return Dragon.daemon();
						} else if (Baron.is_burning()) {
							GrutaPasadizo.PC_is_coming_in = true;
							print "Corres desorientado, aplastando los huesos de aquellos que no tuvieron mejor suerte que tú al plantar cara al dragón.";
						} else {
							GrutaPasadizo.PC_is_coming_in = true;
							print "Haces lo posible por evitar los restos humanos. A cada paso, sin embargo, el chasquido de huesos aplastados bajo tus botas.";
						}
						new_line;
				}
				return self.AdjacentRoom::before();
		],
;

!! @GrutaPasadizo
Indoors	GrutaPasadizo
 has	male,
 with	short_name [;
			print "Un pequeño [](pasadizo)";
			return true;
		],
		name_f 'caverna' 'cueva' 'gruta',
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		!!------------------
		b_to [;
			<<Enter DirGrutaVestibulo>>;
		],
		before [;
			Listen:
				if (noun == 0 or self) {
					print "Puedes escuchar una especie de murmullo ronco que llega desde[if:Utils.is_compass_enabled()] [el n_obj][else] [el DirGrutaDragon][fi].";
					new_line;
					return true;
				} else if (TestScope(noun, player) && ~~noun.before()) {
					print "Prestas atención [al noun] pero no escuchas nada extraño. Nada aparte del crepitar del fuego y el murmullo ronco que llegan desde[if:Utils.is_compass_enabled()] [el n_obj][else] [el DirGrutaDragon][fi].";
					new_line;
					return true;
				}
			Smell:
				if (noun == 0 or self) {
					print "El penetrante olor del azufre llena la [self](cueva).";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		description [;
			if (Baron.is_burning()) {
				print "El mundo a tu alrededor es sólo un bosquejo agónico bajo [el Fuego].[if:Utils.is_compass_enabled()] [Al n_obj][else] [in_obj](Adentro)[fi]: el dragón. [if:Utils.is_compass_enabled()] [Al s_obj][else] [out_obj](Afuera)[fi]: la salida.";
			} else {
				print "El techo del [self](corredor) es tan bajo que tienes que inclinarte para poder pasar. Una capa de [Cenizas](cenizas:REMARKABLE_OBJECT) amortigua tus pisadas. Desde [la DirGrutaDragon], en el extremo[if:Utils.is_compass_enabled()] [n_obj][fi] del [self](pasillo), la potente luz [del GrutaPasadizoFuego] y oleadas de un calor infernal";
				if (self.PC_is_coming_in) {
					print " te golpean en plena cara";
				} else {
					print " te abrasan la espalda";
				}
				print " y provocan que estés empapado en sudor dentro de tu [Armadura](armadura). [El DirGrutaVestibulo][if:Utils.is_compass_enabled()] [al s_obj][fi] se antoja ahora un remanso de frescor.";
			}
			new_line;
			return true;
		],
		exits [;
			if (self.PC_is_coming_in) {
				print "Tienes [el DirGrutaVestibulo] a tu espalda y [el DirGrutaDragon] frente a ti.";
			} else {
				print "Tienes [el DirGrutaVestibulo] frente a ti y [el DirGrutaDragon] a tu espalda.";
			}
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirGrutaDragon>>;
		],
		in_to [;
			<<Enter DirGrutaDragon>>;
		],
		n_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaDragon>>;
			else return self.cant_go();
		],
		out_to [;
			<<Enter DirGrutaVestibulo>>;
		],
		!! Registra si el Barón está entrando o saliendo de la cueva:
		PC_is_coming_in true,
		s_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaVestibulo>>;
			else return self.cant_go();
		],
;

!! @Cenizas
Atrezzo	-> Cenizas "cenizas"
 has	female pluralname transparent,
 with	name_f 'ceniza',
		name_fp 'cenizas',
		gender G_FEMENINO + G_PLURAL,
		adjectives 'capa' 'de' 'suelo',
		!!------------------
		before [;
			Take:
				if (~~self.tried_to_take) {
					self.tried_to_take = true;
					print "Te agachas para coger un puñado de cenizas del suelo, pero están tan comprimidas que sólo consigues manchar ligeramente el guantelete con el fino polvo.";
				} else {
					print "Están tan comprimidas que resulta imposible.";
				}
				new_line;
				return true;
		],
		description [;
			print "Una gruesa capa de cenizas cubre el suelo. Están compactadas hasta tal punto que tus pies apenas dejan huella.";
			new_line;
			return true;
		],
		react_before [;
			if (noun == d_obj) {
				noun = self;
			}
		],
		reset [;
			self.tried_to_take = false;
		],
 private
		tried_to_take false,
;

Atrezzo	-> -> "huellas"
 has	female pluralname,
 with	name_f 'huella',
		name_fp 'huellas',
		name_m 'rastro',
		gender G_FEMENINO + G_PLURAL,
		!!------------------
		description [;
			print "Prácticamente invisibles en la compacta capa de [Cenizas].";
			new_line;
			return true;
		],
;

!! @GrutaPasadizoFuego
Atrezzo	-> GrutaPasadizoFuego "fuego"
 class	Background,
 has	light male,
 with	name_f 'claridad' 'llama' 'luz',
		name_fp 'llamas',
		name_m 'fuego' 'fulgor' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		!!------------------
		before [;
			Blow,
			SwitchOff:
				print "Nadie podría apagar un fuego como ese.";
				new_line;
				return true;
		],
		description [;
			print "Lenguas de fuego mayores que cualquier hombre iluminan el pasadizo desde [la DirGrutaDragon].";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	3)	Guarida del dragón
!!
!!------------------------------------------------------------------------------

!! @DirGrutaDragon
AdjacentRoom	DirGrutaDragon
 has	female,
 with	found_in GrutaPasadizo,
		short_name [;
			print "[](guarida:DIRECTION_OBJECT) del dragón";
			return true;
		],
		name_f 'guarida' 'madriguera',
		name_m 'cubil' 'nido',
		gender G_FEMENINO,
		adjectives 'bestia' 'dragon' 'monstruo',
		!!------------------
		adjacent_room GrutaDragon,
		before [;
			Enter:
				if (Baron.is_burning()) {
					print "Aullando por culpa de las quemaduras e incapaz de pensar con claridad, entras corriendo de nuevo en la guarida del dragón.";
				!! TODO - magic numbers
				} else if (Dragon.counter == 3) {
					print "Lo piensas mejor y decides plantar cara a la bestia, pero quedas congelado por la visión.";
					new_line;
					return Dragon.daemon();
				} else if (Baron.is_running_away()) {
					print "Recompones el ánimo tras unos instantes. No puedes huir así. No eres un cobarde. Estás dispuesto a plantar cara.";
				} else if (self.PC_hesitates) {
					self.PC_hesitates = false;
					Tutorial.hint_hesitations();
					print "La entrada al cubil parece la entrada misma a los avernos. Existen multitud de leyendas acerca de cómo el monstruo llegó a vivir ahí, pero lo cierto es que nadie lo sabe con certeza. Algunos sabios afirman que el dragón es tan antiguo como la propia Tierra y que ha habitado en las profundidades de la gruta desde el origen de los tiempos.
					^^
					Por un momento dudas, preguntándote si podrás hallar en tu interior el valor necesario para dar el paso.";
					new_line;
					return true;
				} else {
					!! Se registran las elecciones del usuario:
					if (Percunos in player
						&& Casco in player && Casco has worn
						&& CascoVisera has open) {
						give E1_CodigoCaballeria on;
					}
					if (Percunos in player
						&& Casco in player && Casco has worn
						&& CascoVisera has ~~open) {
						give E1_MasValePrevenir on;
					}
					if (Percunos notin player
						&& (Casco notin player || Casco hasnt worn)) {
						give E1_Berserker on;
					}
					!! Comprueba el hacha:
					if (IndirectlyContains(player, Percunos)) {
						print "Aferrando el [Percunos](hacha:OTHER_OBJECT) con firmeza";
					} else {
						print "Desarmado";
					}
					!! Comprueba el casco:
					if (IndirectlyContains(player, Casco) && Casco has worn) {
						if (CascoVisera has open) {
							print ", el visor del [Casco](casco:OTHER_OBJECT) abierto —tal y como dictan los códigos—";
						} else {
							print ", el visor del [Casco](casco:OTHER_OBJECT) cerrado —no es lo más honorable pero, sin duda, sí lo más prudente—";
						}
					} else {
						print ", arrogante y bravo sin tu casco";
					}
					print ", apartas los temores a un lado y te dispones a encarar a la bestia.";
				}
				new_line;
				return self.AdjacentRoom::before();
		],
 private
		PC_hesitates true,
;

!! @GrutaDragon
Indoors	GrutaDragon
 has	male,
 with	short_name [;
			print "Enorme [](salón) subterráneo";
			return true;
		],
		name_f 'caverna' 'cueva' 'estancia' 'gruta' 'guarida' 'madriguera'
			'sala',
		name_m 'cubil' 'nido' 'salon',
		gender G_MASCULINO,
		adjectives 'dragon' 'enorme' 'gran' 'grande' 'subterraneo',
		!!------------------
		b_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		before [;
			Listen:
				if (noun == 0 or self) {
					!! <<Listen Dragon>> !! TODO
				} else if (TestScope(noun, player) && ~~noun.before()) {
					print "El estruendo de las [](llamas) y del propio [Dragon] apagan cualquier otro sonido.";
					new_line;
					return true;
				}
			Smell:
				if (noun == 0 or self) {
					print "El penetrante olor del azufre llena la [self](cueva).";
				} else {
					print "El olor del azufre lo impregna todo.";
				}
				new_line;
				return true;
		],
		cant_go [;
			if (Baron.is_burning()) {
				return Grietas.PC_falls();
			}
			return self.Room::cant_go();
		],
		exits [;
			print "La boca [del DirGrutaPasadizo] se abre en una de las paredes de roca del enorme [self](salón).";
			new_line;
			return true;
		],
		!! El atributo 'f_to' resultaría redundante:
		!! f_to [;
		!! 	return self.cant_go();
		!! ],
		initial [;
			if (self hasnt visited) {
				Baron.is_running_away = true;
				StartDaemon(Dragon);
			}
		],
		description [;
			if (Baron.is_burning()) {
				print "Entre la vorágine de [Fuego](llamas:OTHER_OBJECT) sólo aciertas a distinguir imágenes fragmentadas; [Grietas](fisuras:REMARKABLE_OBJECT) en la roca, la mancha carmesí del [Dragon], el pequeño punto de oscuridad que señala la boca [del DirGrutaPasadizo].";
			} else {
				print "[if:Utils.is_compass_enabled()]El pasadizo [s_obj][else][El DirGrutaPasadizo][fi] desemboca en lo que parece una [self](gruta) natural. Es enorme, mucho mayor incluso que la sala del trono del Emperador. Desde infinidad de [Grietas](grietas:REMARKABLE_OBJECT) y [Grietas](fisuras) abiertas en la roca emergen [GrutaDragonFuego](llamaradas:REMARKABLE_OBJECT) gigantescas. La luz aquí dentro es más brillante que el mediodía y el calor resulta insoportable.";
			}
			new_line;
			return true;
		],
		out_to [;
			<<Enter DirGrutaPasadizo>>;
		],
		s_to [;
			if (Utils.is_compass_enabled()) <<Enter DirGrutaPasadizo>>;
			else return self.cant_go();
		],
;

!! @Dragon
Character	-> Dragon "dragón"
 has	concealed male,
 with	name_f 'bestia' 'monstruosidad',
		name_m 'dragon' 'engendro' 'monstruo',
		gender G_MASCULINO,
		adjectives 'carmesi' 'roja' 'rojo',
		article "el",
		!!------------------
		before [
			previous_style;
			DefaultAction:
				if (self has described) {
					previous_style = Utils.set_text_style(TEXT_STYLE_PARSER);
					print (string) TEXT_STYLE_PARSER_PREFIX;
					print "ataca ", (al_) self;
					PrintToBuffer(utility_array_a, INPUT_BUFFER_LEN, self);
					PrintCharacterArray(utility_array_a);
					print (string) TEXT_STYLE_PARSER_SUFIX;
					Utils.set_text_style(previous_style);
					new_line;
					<<Attack self>>;
				}
		],
		daemon [;
			!! El daemon gestiona el movimiento del dragón, con sus intentos
			!! por incinerar o comerse al Barón:
			switch (location) {
				GrutaDragon:
					if (self.counter == 0) {
						new_line;
						print "La mole de escamas rojas [del self] yace en el suelo en mitad de la [GrutaDragon](cueva), observándote fijamente con su malévolo ojo amarillo.";
						new_line;
					} else if (self.counter == 1) {
						new_line;
						print "[El self] se incorpora con una agilidad sorprendente para su corpulencia, provocando un temblor que sacude toda la [GrutaDragon](cueva) y que a punto está de tirarte al suelo. Sólo gracias a tus rápidos reflejos y a décadas de experiencia llevando armaduras pesadas eres capaz de mantener el equilibrio.";
						new_line;
					} else if (self.counter == 2) {
						self.spit_fire();
						new_line;
						print "Un inmenso chorro de [Fuego] sale escupido de la boca [del self] y te envuelve por completo antes siquiera de que puedas pensar en apartarte. El dolor lo ocupa todo de pronto. Un dolor como nunca antes lo habías sentido.";
						new_line;
					} else if (self.counter >= 4) {
						give E1_YEseRuido on;
						new_line;
						print "Desorientado por el torbellino de llamas pierdes de vista al monstruo. Todo es fuego y dolor a tu alrededor. Cuando alcanzas a ver de nuevo su borrón rojo lo tienes ya encima y es demasiado tarde para reaccionar; sus dientes atraviesan la armadura con la misma facilidad con la que atravesarían la piel de una manzana. Tu destino está sentenciado. Las mandíbulas de la bestia se cierran con un crujido repugnante —¿acaso la armadura? ¿tus propios huesos?— y un torrente de sangre te inunda la boca. El alivio de la muerte no se hace esperar.";
						new_line;
						print "^^^", (parser) "Pulsa una tecla.";
						Utils.press_any_key();
						return EndSceneOne();
					}
				GrutaPasadizo:
					if (~~Baron.is_burning()) {
						if (self.counter == 1) {
							new_line;
							print "El monstruo lanza un chillido estremecedor que perfora tus oídos. No está dispuesto a dejarte escapar.";
							new_line;
						}
						if (self.counter == 2) {
							new_line;
							print "Distingues la figura [del self] más allá de la abertura del fondo del [GrutaPasadizo](pasillo). Ves su cabeza. Ves el baile de las llamas tras sus afilados dientes blancos.";
							new_line;
						} else if (self.counter >= 3) {
							self.spit_fire();
							new_line;
							print "Un inmenso chorro de [Fuego] sale despedido desde la boca abierta [del self] al fondo del corredor y te envuelve por completo. El dolor lo ocupa todo de pronto. Un dolor como nunca antes lo habías sentido.";
							new_line;
						}
					}
				GrutaVestibulo:
					if (~~Baron.is_burning()) {
						if (self.counter == 2) {
							new_line;
							print "Un estruendo ensordecedor hace temblar las paredes de la cueva. El olor a azufre es más intenso de pronto. Cuesta respirar.";
							new_line;
						} else if (self.counter == 3) {
							new_line;
							print "Ves un fogonazo de luz a tu espalda. El [DirGrutaPasadizo](pasillo) que conduce a la guarida del dragón se ha llenado de pronto con un torrente de fuego que se propaga a toda velocidad hacia el vestíbulo, como un tentáculo ardiente en busca de su presa.";
							new_line;
						} else if (self.counter >= 4) {
							self.spit_fire();
							new_line;
							print "Y de nuevo el alarido sobrenatural y aterrador de la bestia, tan sólo un instante antes de que la tempestad de llamas estalle en el interior del vestíbulo. No hay escapatoria. El dolor que sucede [al Fuego] supera cualquier límite concebible.";
							new_line;
						}
					}
			}
			self.counter++;
			return true;
		],
		description [;
			!! TODO - ¿si el Barón está ardiendo?
			!! TODO - ¿si se examina de nuevo?
			if (self hasnt described) {
				print "Aunque creías haberte preparado para lo peor no estabas preparado para esto.
				^^
				Es grande, desde luego —calculas al menos una treintena de varas desde la punta de la cola hasta la testa—, pero ya te has enfrentado a monstruos colosales antes. Sus escamas rojas refulgen con el mismo brillo del acero endurecido y pareciera que ningún arma corriente pudiera atravesarlas";
				if (Percunos in player && Percunos has described) {
					print ", pero *Percunos* no es ningún hacha ordinario";
				} else if (Percunos in player && Percunos hasnt described) {
					print ", pero el tuyo no es ningún hacha ordinario";
				} else {
					print ", pero ninguna criatura es invulnerable a un golpe certero";
				}
				print ". Y sin embargo… el *fuego*. Ése que se adivina en sus ojos y en su garganta, el baile de llamas tras la blanca hilera de dientes afilados. Ese fuego imbuye tu alma con un horror como nunca lo ha hecho nada antes. No se trata de otro engendro escupe-fuego cualquiera, ni tiene nada que ver con un elemental —esas criaturas irracionales en quienes se encarnan las fuerzas primordiales de la naturaleza—. No, esto no es natural. Esto es mucho más aterrador, más insondable, más perverso. Sabes muy bien que ese fuego no podrá ser sometido en absoluto, ningún hombre podrá jamás doblegarlo y obligarlo a ceder a su voluntad; pues lo reconoces y cualquier otra persona lo reconocería también. Este monstruo, esta bestia, este fuego abrasador habita en el rincón más profundo del alma de todos nosotros. Siempre lo ha hecho y siempre lo hará. Y sabes que no hay modo de derrotarlo.";
			} else {
				!! TODO
				return true;
			}
			new_line;
			return true;
		],
		life [;
			Attack:
				if (Baron.is_burning()) {
					print "Con la consciencia completamente aletargada por el dolor tu cuerpo parece actuar solo, llevado sólo por el instinto, y como fuera de ti le observas recomponerse y prepararse para atacar. El monstruo debe morir sin importar el precio. Por un momento alcanzas a ver entre las llamas los orbes maliciosos de sus ojos";
					if (IndirectlyContains(player, Percunos)) {
						print " y levantas el hacha dispuesto a descargarlo directamente entre ellos";
					} else {
						print " y preparas el puño dispuesto a intentar cegarlo";
					}
					print ". Entonces la cola del dragón se agita y te golpea en un costado, enviándote volando por la gruta en un vertiginoso arco que finaliza abruptamente con tu cabeza impactando contra la pared";
					if (IndirectlyContains(player, casco) && Casco has worn) {
						print ". El casco amortigua en parte el golpe y evita que pierdas el conocimiento. Aún llegas a ver la cabeza grotesca acercándose a ti. Su boca abierta de par en par. Una nueva tempestad de fuego.
						^^
						La muerte llega rápido.";
					} else {
						print ". El cráneo se fractura en el acto y todo se vuelve negro.";
					}
					new_line;
					give E1_GuerreroObstinado on;
					print "^^^", (parser) "Pulsa una tecla.";
					Utils.press_any_key();
					return EndSceneOne();
				} else {
					self.spit_fire();
					if (IndirectlyContains(player, Percunos)) {
						print "Alzas el hacha con ambas manos";
					} else {
						print "Levantas un furioso puño";
					}
					print ". —¡Escucha bien, bestia! —gritas, sorprendido de la seguridad de tus propias palabras. —¡Ha llegado tu hora! —y, por un instante, la estampa resulta heroica; la de un auténtico caballero sacado de un cuento de hadas.
					^^
					El monstruo te observa impasible mientras cargas contra él. Sus dientes largos como una vara, pero cargas a pesar de todo, espoleado por una rabia suicida. En ese momento, veloz y repentino como un relámpago, un surtidor de fuego estalla desde el interior de su boca y te envuelve por completo. El dolor lo ocupa todo de pronto. Un dolor como nunca antes habías imaginado.";
					new_line;
					new_line;
					GrutaDragon.description();
					return true;
				}
		],
		!!------------------
		counter 0,
 private
		spit_fire [;
			StartDaemon(Baron);
			move GrutaDragonFuego to Limbo;
			move GrutaPasadizoFuego to Limbo;
			move GrutaVestibuloFuego to Limbo;
			move Fuego to Baron;
			Baron.is_burning = true;
			if (self.counter < 3) self.counter = 3;
		],
		!!------------------
;

!! Object grot_draak "dragon" GrutaDragon
!! 	     Attack, ThrowAt:
!!
!! 	       if(self.attack > 0 || Baron.is_burning() > 0) {
!! 	         print "With heroic willpower you resist the numbing pain and charge again: the monster must die, though your life be the price. Through the flames, you see the pale malicious circles of its eyes ";
!! 		 if(parent(Percunos) == player) print "and raise your axe to plant it right between them.";
!! 		 if(parent(Percunos) ~= player) print "and raise your fists in order to plant them right in them, blinding the monster.";
!! 		 print " But suddenly the dragon's tail swings in from the side and hits you, sending you flying through the hall in dizzying arc that ends when your head crashes into a wall.";
!! 		 if(Casco has worn) print " Your helmet softens the blow, and you quickly regain your senses. Unfortunately, when you open your eyes, the dragon's monstrous head is already approaching you, its mouth wide open--and once again it bathes you in deadly fire. Death comes quickly.^";
!! 		 if(Casco hasnt worn) print " Your skull fractures immediately, and everything goes black.^";
!! 		 EndSceneOne();
!! 		 return true;
!! 	        };
!!
!! 	       if(self.attack == 0 && Baron.is_burning() == 0) {
!! 	          if(parent(Percunos) == player) print "Raising the axe, you step towards the dragon. ~Your knell has rung,~ you inform it, surprised at your own confidence. ~It's time to die.~^^";
!!
!! 	          if(parent(Percunos) ~= player) print "Raising you armoured fists, you step towards the dragon. ~Your knell has rung,~ you inform it, surprised at your own confidence. ~It's time to die.~^^";
!!
!! 		  print "The monster looks at you impassively. You cannot help but stare at its double rows of arm-long teeth as you charge, but charge you do, screaming with justified rage. Then--as fast and sudden as lightning--a jet of fire bursts from dragon's the mouth and envelops you from head to toe. The pain is beyond comprehension.^";
!! 		  Baron.is_burning = 1;
!! 	        };
!!
!! 	       self.attack++;
!! 	       return true;
!! 	   ]
!! ;

!! @Grietas
!! TODO - Comprobar si el Barón se había bajado el visor del casco. Si no es
!! así, la primera llamarada del dragón lo ciega y es más fácil que tropiece y
!! caiga dentro de una de las grietas
Atrezzo	-> Grietas "grietas"
 has	container enterable female open ~openable pluralname,
 with	name_f 'fisura' 'grieta' 'hendedura' 'hendidura',
		name_fp 'fisuras' 'grietas' 'hendeduras' 'hendiduras',
		gender G_FEMENINO + G_PLURAL,
		!!------------------
		before [
			intentional;
			Enter:
				if (Baron.is_burning()) {
					intentional = true;
					return self.PC_falls(intentional);
				} else {
					print "Ahí no aguarda más que la muerte.";
				}
				new_line;
				return true;
			JumpOver:
				<<Enter self>>;
		],
		description [;
			print "Amplias grietas perforan la roca por doquier y escupen al aire unas [](llamaradas) más altas que tú mismo. Tienes que tener cuidado con dónde pones los pies para evitar precipitarte dentro de alguna por accidente.";
			new_line;
			return true;
		],
		PC_falls [ intentional;
			give E1_ExcavandoDemasiadoProfundo on;
			if (intentional) {
				print "Enloquecido por el dolor prefieres poner fin al sufrimiento lo antes posible y saltas al interior de una de las grandes fisuras que atraviesan el suelo. Caes. A través del fuego y del humo, caes. Las mismas llamas que te consumen iluminan las paredes perfectamente verticales de la hendidura, mucho más profunda de lo que podías imaginar. Sigues cayendo y alumbrando la oscuridad como una antorcha humana hasta que de pronto el estrecho espacio se abre en una gigantesca bóveda subterránea con un lago de magma en el fondo. Por una fracción de latido de corazón quedas fascinado ante la belleza sobrecogedora de este mundo subterráneo. Entonces impactas salvajemente contra la roca fundida. Todo tu cuerpo es abrasado y arde y tu alma queda transformada en fuego, en llamas, en calor, en luz.";
			} else {
				print "Echas a correr presa del pánico, cegado por el fuego y por el humo, incapaz de ver dónde pones los pies. Tropiezas y caes y vuelves a levantarte y tratas de continuar con tu carrera impelido por un terror agónico y primigenio mientras las llamas consumen tu cuerpo. Tienes que alejarte de la bestia y de su aliento mortal; tienes que alejarte del dolor. Entonces das un paso en la nada. El suelo desaparece bajo tus pies en una de las inmensas fisuras que atraviesan la gruta, y caes una vez más. Intentas aferrarte a algo desesperadamente, lo que sea, pero las paredes de la hendidura se prolongan perfectamente verticales, sin ningún saliente que pueda detener tu descenso hacia las profundidades. Caes. El aire está lleno de humo y vapores de sulfuro. Notas cómo penetran en tus pulmones con cada inspiración. Sigues cayendo cuando empiezas a ver un resplandor más abajo. Entonces pierdes el conocimiento, sin llegar a descubrir lo que sea que provocaba aquella luz.";
			}
			new_line;
			print "^^^", (parser) "Pulsa una tecla.";
			Utils.press_any_key();
			return EndSceneOne();
		],
		react_before [;
			VagueGo:
				if (Baron.is_burning()) {
					return self.PC_falls();
				}
		],
;

!! @GrutaDragonFuego
Atrezzo	-> GrutaDragonFuego "fuego"
 class	Background,
 has	light male,
 with	name_f 'claridad' 'llama' 'llamarada' 'luz',
		name_fp 'llamas' 'llamaradas',
		name_m 'fuego' 'fulgor' 'resplandor',
		name_mp 'fuegos',
		gender G_MASCULINO,
		adjectives 'lengua' 'lenguas',
		!!------------------
		before [;
			!! TODO Touch…
			Blow,
			SwitchOff:
				print "Nadie podría apagar un fuego como ese.";
				new_line;
				return true;
		],
		description [;
			SetPronoun('-las', self);
			SetPronoun('ellas', self);
			print "Llamas gigantescas brotan del suelo allá dónde mires.";
			new_line;
			return true;
		],
;




    !! Object grot_fake_draak "dragon" GrutaPasadizo
    !!   with name 'dragon',
    !!        description
    !!          "The dragon is in the large hall to the north.",
	!!    before [;
	!!      Examine: return false;
	!!      default: "You will first have to enter the hall to the north.";
	!!    ],
	!!    life [;
	!!      default: "You will first have to enter the hall to the north.";
	!!    ],
    !!    has male animate scenery;
