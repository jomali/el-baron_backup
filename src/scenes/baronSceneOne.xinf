

!!==============================================================================
!!
!!	EL BARÓN
!!	Escena primera
!!
!!==============================================================================
!!
!!	Archivo:		baronSceneOne.xinf
!!	Autor(es):		Victor Gijsbers
!!					J. Francisco Martín <jfm.lisaso@gmail.com>
!!	Idioma:			ES (Español)
!!	Sistema:		Inform-INFSP 6
!!	Plataforma:		Máquina-Z/Glulx
!!	Versión:		0.0
!!	Fecha:			2021/05/XX
!!
!!------------------------------------------------------------------------------
!!
!!	Este archivo forma parte de la ficción interactiva EL BARÓN.
!!
!!	EL BARÓN es software libre: usted puede redistribuirlo y/o
!!	modificarlo bajo los términos de la Licencia Pública General
!!	GNU publicada por la Fundación para el Software Libre, ya
!!	sea la versión 3 de la Licencia, o (a su elección) cualquier
!!	versión posterior.
!!
!!	EL BARÓN se distribuye con la esperanza de que sea útil,
!!	pero SIN GARANTÍA ALGUNA; ni siquiera la garantía implícita
!!	MERCANTIL o de APTITUD PARA UN PROPÓSITO DETERMINADO.
!!	Consulte los detalles de la Licencia Pública General GNU
!!	para más información.
!!
!!	Debería haber recibido una copia de la Licencia Pública
!!	General GNU junto a EL BARÓN. En caso contrario, consulte
!!	<http://www.gnu.org/licenses/>.
!!
!!	Copyright (c) 2004, Victor Gijsbers
!!	Copyright (c) 2021, J. Francisco Martín
!!
!!------------------------------------------------------------------------------


!!==============================================================================
!!
!!	0)
!!
!!------------------------------------------------------------------------------

!! @Armadura
Item Armadura Limbo
	class
		Clothes,
	has
		male,
	with
		short_name [;
			print "conjunto completo de [](armadura:REMARKABLE_OBJECT)";
			return true;
		],
		name_f 'armadura' 'cota' 'malla',
		name_fp 'cintas' 'mallas' 'placas',
		name_m 'guantelete',
		name_mp 'guanteletes',
		gender G_MASCULINO,
		adjectives 'acero' 'completo' 'conjunto' 'cuero',
		!!
		before [;
			Disrobe:
				if (Baron.is_burning()) {
					print "El metal fundido de la [self](armadura:OTHER_OBJECT) te perfora el cuerpo. Querrías quitártela. Arrancarte la piel. Pero no tienes cómo.";
				} else {
					print "No puedes quitarte la [self](armadura:OTHER_OBJECT) sin ayuda; está asegurada con apretadas cintas de cuero a la espalda.";
				}
				new_line;
				return true;
		],
		description [;
			if (Baron.is_burning()) {
				print "Incluso el acero de la [self](armadura:OTHER_OBJECT) se está fundiendo con el calor del fuego. Sientas las gotas de metal líquido abriéndose paso a través de tu piel como cuchillos.";
			} else {
				if (real_location == GrutaPasadizo or GrutaSalon) {
					print "Por su culpa estás sudando copiosamente, pero jamás";
				} else {
					print "Jamás";
				}
				print " te enfrentarías a la bestia sin un conjunto completo de [self](armadura:OTHER_OBJECT). Gruesas placas de acero sobre cota de mallas —todo ello engrasado concienzudamente—, con lo que te aseguras la mayor protección posible ante cualquier tipo de ataque físico. El conjunto está ajustado por unas cintas de cuero a la espalda.";
			}
			new_line;
			return true;
		],
;

!! @Yelmo
Item Yelmo "yelmo" Limbo
	class
		Clothes,
	has
		male transparent,
	with
		name_m 'casco' 'morrion' 'yelmo',
		gender G_MASCULINO,
		adjectives 'antiguo' 'ennegrecido' 'funcional' 'negro',
		!!
		after [;
			Disrobe:
				move self to parent(player);
				print "Te l[o self] quitas y l[o self] posas en el suelo.";
				new_line;
				return true;
			Take:
				give self worn;
				if (~~self.tried_to_wear) {
					self.tried_to_wear = true;
					print "Recoges [el self] y l[o self] sacudes enérgicamente antes de ponértel[o self], vaciando una cascada de ceniza sobre el suelo. Los sonidos de la cueva parecen intensificarse en su interior.";
				} else {
					print "Recoges [el self] del suelo y te l[o self] pones.";
				}
				new_line;
				return true;
		],
		before [;
			Close:
				<<Close YelmoVisera>>;
			Drop:
				if (self in player && self has worn) {
					<<Disrobe self>>;
				}
			Open:
				<<Open YelmoVisera>>;
			Wear:
				if (TestScope(self, player) && self notin player) {
					<<Take self>>;
				}
		],
		description [;
			if (Baron.is_burning()) {
				print "De algún modo antinatural, el metal [del self] permanece frío en mitad del infierno.";
			} else {
				if (self in player && self has worn) {
					if (YelmoVisera has open) {
						print "[La YelmoVisera] está abiert[a YelmoVisera], dejando libre tu campo visual pero exponiendo tu rostro ante un eventual ataque.";
					} else {
						print "[La YelmoVisera] está cerrad[a YelmoVisera], ofreciendo la máxima protección.";
					}
				} else {
					print "[Un self] ennegrecido, sin adornos de ningún tipo.";
					if (YelmoVisera has open) {
						print " Tiene [la YelmoVisera] abiert[a YelmoVisera].";
					} else {
						print " Tiene [la YelmoVisera] cerrad[a YelmoVisera].";
					}

				}
			}
			new_line;
			return true;
		],
	private
		tried_to_wear false,
;

!! @YelmoVisera
Item -> -> YelmoVisera "visera"
	has
		female open openable,
	with
		name_f 'visera',
		name_m 'visor',
		gender G_FEMENINO,
		adjectives 'casco',
		!!
		after [;
			Close:
				print "Cierras la [self](visera) [del Yelmo].";
				new_line;
				return true;
			Open:
				print "Abres la [self](visera) [del Yelmo].";
				new_line;
				return true;
		],
		before [;
			DefaultAction:
				if (self has open) {
					<<Close self>>;
				} else {
					<<Open self>>;
				}
			Examine:
				<<Examine Yelmo>>;
		],
;

!! @Percunos
Item Percunos Limbo
	has
		male remarkable,
	with
		short_name [;
			print "[](hacha:REMARKABLE_OBJECT) de guerra";
			return true;
		],
		name_m 'arma' 'hacha' 'percunos' 'perkwunos',
		gender G_MASCULINO,
		adjectives 'batalla' 'guerra',
		article "tu",
		!!
		description [;
			print "Para esta gesta te has traído el más pesado de tus hachas de guerra; *Percunos*. Pocos pueden levantar su peso, y menos aún manejarlo con la misma velocidad y precisión mortíferas con las que tú lo haces.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	1)	En el abismo
!!
!!------------------------------------------------------------------------------

!! @Abismo
Indoors Abismo
	has
		concealed male,
	with
		short_name [;
			print "En el abismo";
			return true;
		],
		gender G_MASCULINO,
		!!
		description [;
			Tutorial.hint_highlight_key_a();
			if (AbismoOscuridad has described) {
				print "Distingues un débil [AbismoResplandor](fulgor) entre las sombras.";
			} else {
				print "Todo es [AbismoOscuridad] a tu alrededor. El universo entero reducido a un vacío negro.";
			}
			new_line;
			return true;
		],
		exits [;
			if (AbismoOscuridad has described) {
				print "Todo cuanto puedes distinguir en [la AbismoOscuridad] es ese [AbismoResplandor] indefinido que llega desde algún lugar más [f_obj].";
			} else {
				print "No sabrías hacia dónde dirigirte en [la AbismoOscuridad].";
			}
			new_line;
			return true;
		],
		f_to [;
			<<Enter DirAbismoAdelante>>;
		],
;

!! @DirAbismoAdelante
AdjacentRoom -> DirAbismoAdelante
	has
		male,
	with
		adjacent_room GrutaVestibulo,
		before [
			previous_style;
			Enter:
				self.AdjacentRoom::onChangeLocation();
				if (AbismoOscuridad hasnt described) {
					print "Alcanzas a distinguir un resplandor entre las sombras. ";
				} else {
					print "Ha llegado el momento. Después de todo tú eres la única persona que puede acabar con esto.
					^^";
				}
				print "Tan pronto das un paso en la dirección de la que proviene la luz, las dos grandes puertas de bronce se cierran a tu espalda con un golpe atronador seguido por el chirrido de cerrojos. Ya no hay marcha atrás. Aferras el [Percunos](hacha:REMARKABLE_OBJECT) con firmeza y das otro paso adelante, hacia el calor…";
				new_line;

				self.AdjacentRoom::before();
				return true;
		],
		description [;
			<<Examine AbismoResplandor>>;
		],
;

!! @AbismoOscuridad
Background -> AbismoOscuridad "oscuridad"
	has
		female on,
	with
		name_f 'abismo' 'negrura' 'oscuridad' 'sombra' 'tiniebla',
		name_fp 'sombras' 'tinieblas',
		name_m 'vacio',
		gender G_FEMENINO,
		adjectives 'negro',
		!!
		description [;
			if (self has described) {
				<<Look>>;
			} else {
				Tutorial.hint_highlight_key_b();
				print "Te dejas perder en el abrazo de las [self](sombras). Solo e inmóvil en mitad de la nada, lejos de obligaciones, de rutinas diarias y de reglas sociales inconcebiblemente complejas y agotadoras, casi desearías poder quedarte aquí para siempre. Descansar. Pero no es más que una idea infantil, por supuesto.
				^^
				Alcanzas a percibir [un AbismoResplandor] que rompe apenas la oscuridad.";
			}
			new_line;
			return true;
		],
		react_before [;
			if (self hasnt on) return false;
			Consult,
			Examine,
			Look,
			LookUnder,
			Read,
			Search:
				if (noun == nothing) return false;
				if (noun == self) return false;
				if (noun == parent(self)) return false;
				if (noun == AbismoResplandor) return false;
				return self.cant_see();
			DefaultAction:
				return false;
			Drop:
				if (noun == nothing) return false;
				if (~~IndirectlyContains(player, noun)) return false;
				print "Si dejas [el noun] aquí es posible que luego no puedas encontrarl[o noun] de nuevo en [la self].";
				new_line;
				return true;
			Inv,
			InvTall,
			InvWide:
				return self.cant_see();
			Rub:
				if (noun == nothing) return false;
				if (noun == self) return false;
				if (noun == parent(self)) return false;
				if (noun ofclass Background) return false;
				print "Es difícil limpiar [el noun] —o cualquier otra cosa— en completa oscuridad.";
				new_line;
				return true;
		],
	private
		cant_see [;
			if (self has described) {
				print "No puedes ver nada en [la self], aparte de [un AbismoResplandor] muy débil que llega desde algún lugar más [f_obj].";
			} else {
				print "No puedes ver nada en [la self].";
			}
			new_line;
			return true;
		],
;

!! @AbismoResplandor
Background -> AbismoResplandor "resplandor"
	has
		male,
	with
		name_f 'luminosidad' 'luz',
		name_m 'fulgor' 'resplandor',
		gender G_MASCULINO,
		adjectives
			'anaranjada' 'anaranjado' 'imprecisa' 'impreciso' 'tenue',
		!!
		description [;
			print "Se trata de una luz parpadeante muy tenue. Llega desde algún lugar más [f_obj].";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	2)	Vestíbulo de la gruta
!!
!!------------------------------------------------------------------------------

!! @GrutaVestibulo
Indoors GrutaVestibulo
	has
		male,
	with
		short_name [;
			print "[](Vestíbulo) de la gruta";
			return true;
		],
		name_f 'antesala' 'caverna' 'cueva' 'estancia' 'gruta' 'sala',
		name_m 'hall' 'recibidor' 'vestibulo',
		gender G_MASCULINO,
		adjectives 'fresca' 'fresco' 'pequena' 'pequeno' 'rectangular',
		!!
		description [;
			print "Esta pequeña sala rectangular ha sido excavada en la roca y sus paredes y su techo han sido recubiertos después con planchas de mármol negro. Dos puertas de bronce sellan la única salida. Frente a ellas, en la pared opuesta del vestíbulo, un estrecho corredor se interna aún más en las ardientes profundidades de la montaña.";
			new_line;
			return true;
		],
		! each_turn [;
		! 	print "[Los GrutaHuesos] del suelo arrojan sombras grotescas en la luz titilante.";
		! 	new_line;
		! 	return true;
		! ],
;

!! @GrutaHuesos
Atrezzo -> GrutaHuesos "huesos"
	has
		~concealed male pluralname ~scenery static,
	with
		name_f 'calavera',
		name_fp 'calaveras',
		name_m 'craneo' 'esqueleto' 'hueso' 'osario',
		name_mp 'craneos' 'esqueletos' 'huesos',
		gender G_MASCULINO + G_PLURAL,
		adjectives
			'despedazado' 'despedazados' 'ennegrecida' 'ennegrecidas'
			'ennegrecido' 'ennegrecidos' 'humana' 'humanas' 'humano' 'humanos'
			'negra' 'negras' 'negro' 'negros',
		!!
		before [;
			Take:
				print "Son los restos de aquellos que vinieron aquí antes que tú, y no tienes ninguna intención de cargar con recuerdos de su fracaso.";
				new_line;
				return true;
		],
		describe [;
			new_line;
			print "Una auténtica alfombra de [self](huesos) y [self](cráneos) ennegrecidos oculta el suelo.";
			new_line;
			return true;
		],
		description [;
			print "El suelo está cubierto de esqueletos despedazados. Negros por el fuego que en algún momento consumió la carne de sus cuerpos.";
			new_line;
			return true;
		],
;


!!==============================================================================
!!
!!	3)	Un pequeño pasadizo
!!
!!------------------------------------------------------------------------------

!! @GrutaPasadizo
Indoors GrutaPasadizo
	has
		male,
	with
		short_name [;
			print "Un pequeño [](pasadizo)";
			return true;
		],
		name_f 'caverna' 'cueva' 'gruta',
		name_m 'corredor' 'pasadizo' 'pasillo',
		gender G_MASCULINO,
		adjectives 'bajo' 'estrecho' 'pequeno',
		!!
;


!!==============================================================================
!!
!!	4)	Enorme salón subterráneo
!!
!!------------------------------------------------------------------------------

!! @GrutaSalon
Indoors GrutaSalon
	has
		male,
	with
		short_name [;
			print "Enorme [](salón) subterráneo";
			return true;
		],
		name_f
			'caverna' 'cueva' 'estancia' 'gruta' 'guarida' 'madriguera' 'sala',
		name_m 'cubil' 'nido' 'salon',
		gender G_MASCULINO,
		adjectives 'dragon' 'enorme' 'gran' 'grande' 'subterraneo',
		!!
;
